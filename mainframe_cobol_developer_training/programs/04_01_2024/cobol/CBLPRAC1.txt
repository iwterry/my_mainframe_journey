       ID DIVISION.
       PROGRAM-ID. CBLPRAC1.

      ************************ DESCRIPTION ******************** 
      *    - THIS COBOL PROGRAM IS USED TO GENERATE 3 PS DATA FILES
      *      THAT CAN BE USED AS INPUT DATA FILES FOR THE COBAL
      *      PROGRAMS "CBLPRAC2", "CBLPRAC3", "CBLPRAC4".
      *    - THE DATA TO CREATE THE DATA FILES WILL COME FROM THE 
      *      SYSIN DD STATEMENT IN THE JCL FILE "JCLPRAC1" THAT IS 
      *      USED TO COMPILE AND RUN THIS PROGRAM.
      *********************************************************
       ENVIRONMENT DIVISION.
       INPUT-OUTPUT SECTION.
       FILE-CONTROL.
           SELECT FILE1 ASSIGN TO DDFILE1
           ORGANIZATION IS SEQUENTIAL
           ACCESS MODE IS SEQUENTIAL
           FILE STATUS IS WS-FILE1-STATUS.

           SELECT FILE2 ASSIGN TO DDFILE2
           ORGANIZATION IS SEQUENTIAL
           ACCESS MODE IS SEQUENTIAL
           FILE STATUS IS WS-FILE2-STATUS.

           SELECT FILE3 ASSIGN TO DDFILE3
           ORGANIZATION IS SEQUENTIAL
           ACCESS MODE IS SEQUENTIAL
           FILE STATUS IS WS-FILE3-STATUS.

       DATA DIVISION.
       FILE SECTION.
       FD FILE1 LABEL RECORD IS STANDARD.
       COPY FILE1CPY
         REPLACING FILE1-REC-LAYOUT BY FILE1-REC-LAYOUT-OUT.

       FD FILE2 LABEL RECORD IS STANDARD.
       COPY FILE2CPY
         REPLACING FILE2-REC-LAYOUT BY FILE2-REC-LAYOUT-OUT.

       FD FILE3 LABEL RECORD IS STANDARD.
       COPY FILE2CPY
         REPLACING FILE2-REC-LAYOUT BY FILE3-REC-LAYOUT-OUT.

       WORKING-STORAGE SECTION.
       COPY FILE1CPY
         REPLACING FILE1-REC-LAYOUT BY FILE1-REC-LAYOUT-IN.

       COPY FILE2CPY
         REPLACING FILE2-REC-LAYOUT BY FILE2-AND-3-REC-LAYOUT-IN.

       01 WS-FILE1-STATUS PIC X(02) VALUE SPACE.
       01 WS-FILE2-STATUS PIC X(02) VALUE SPACE.
       01 WS-FILE3-STATUS PIC X(02) VALUE SPACE.

       01 WS-NUM-RECS-TO-PROCESS PIC 9(02) VALUE ZERO.
       01 WS-NUM-INPUT-RECS PIC 9(02) VALUE ZERO.
       01 WS-NUM-OUTPUT-RECS PIC 9(02) VALUE ZERO.

       01 WS-ABENDPGM PIC X(08) VALUE 'ABENDPGM'.
       01 WS-I PIC 9(02) VALUE ZERO.


       PROCEDURE DIVISION.
       000-MAIN-PARA.
           DISPLAY 'CBLPRAC1 STARTED'.

           OPEN OUTPUT FILE1 FILE2 FILE3.
           IF WS-FILE1-STATUS NOT = '00' OR
              WS-FILE2-STATUS NOT = '00' OR
              WS-FILE3-STATUS NOT = '00'

             DISPLAY 'ERROR IN 000-MAIN-PARA'
             DISPLAY 'ERROR IN OPENING FILE(S)'
             DISPLAY 'FILE1 STATUS: ' WS-FILE1-STATUS
             DISPLAY 'FILE2 STATUS: ' WS-FILE2-STATUS
             DISPLAY 'FILE3 STATUS: ' WS-FILE3-STATUS
             CALL WS-ABENDPGM
           END-IF.

      *************** FILE 1 ************
           INITIALIZE FILE1-REC-LAYOUT-IN FILE1-REC-LAYOUT-OUT.
           DISPLAY 'NOW DOING FILE 1'.
           PERFORM 010-FILE1-PARA THRU 010-EXIT-PARA.
           CLOSE FILE1.
           DISPLAY 'NUM INPUT RECS: ' WS-NUM-INPUT-RECS.
           DISPLAY 'NUM OUTPUT RECS: ' WS-NUM-OUTPUT-RECS.
      *************** FILE 2 ************
           INITIALIZE FILE2-AND-3-REC-LAYOUT-IN FILE2-REC-LAYOUT-OUT.
           MOVE ZERO TO WS-NUM-INPUT-RECS WS-NUM-OUTPUT-RECS.
           DISPLAY 'NOW DOING FILE 2'.
           PERFORM 020-FILE2-PARA THRU 020-EXIT-PARA.
           CLOSE FILE2.
           DISPLAY 'NUM INPUT RECS: ' WS-NUM-INPUT-RECS.
           DISPLAY 'NUM OUTPUT RECS: ' WS-NUM-OUTPUT-RECS.
      *************** FILE 3 ************
           INITIALIZE FILE2-AND-3-REC-LAYOUT-IN FILE3-REC-LAYOUT-OUT.
           MOVE ZERO TO WS-NUM-INPUT-RECS WS-NUM-OUTPUT-RECS.
           DISPLAY 'NOW DOING FILE 3'.
           PERFORM 030-FILE3-PARA THRU 030-EXIT-PARA.
           CLOSE FILE3.
           DISPLAY 'NUM INPUT RECS: ' WS-NUM-INPUT-RECS.
           DISPLAY 'NUM OUTPUT RECS: ' WS-NUM-OUTPUT-RECS.

           STOP RUN.

       010-FILE1-PARA.
           ACCEPT WS-NUM-RECS-TO-PROCESS.
           PERFORM VARYING WS-I FROM 1 BY 1
             UNTIL WS-I > WS-NUM-RECS-TO-PROCESS

             ACCEPT PERSON-ID OF FILE1-REC-LAYOUT-IN
             ACCEPT PERSON-NAME OF FILE1-REC-LAYOUT-IN
             ADD 1 TO WS-NUM-INPUT-RECS

             MOVE PERSON-ID OF FILE1-REC-LAYOUT-IN
               TO PERSON-ID OF FILE1-REC-LAYOUT-OUT
             MOVE PERSON-NAME OF FILE1-REC-LAYOUT-IN
               TO PERSON-NAME OF FILE1-REC-LAYOUT-OUT

            WRITE FILE1-REC-LAYOUT-OUT
            IF WS-FILE1-STATUS NOT = '00'
              DISPLAY 'ERROR IN 000-FILE1-PARA'
              DISPLAY 'ERROR WRITING TO FILE1'
              DISPLAY 'PERSON-ID: ' PERSON-ID OF FILE1-REC-LAYOUT-IN
              DISPLAY 'FILE STATUS: ' WS-FILE1-STATUS
              CALL WS-ABENDPGM
            END-IF
            ADD 1 TO WS-NUM-OUTPUT-RECS
           END-PERFORM.

       010-EXIT-PARA. EXIT.


       020-FILE2-PARA.
           ACCEPT WS-NUM-RECS-TO-PROCESS.
           PERFORM VARYING WS-I FROM 1 BY 1
             UNTIL WS-I > WS-NUM-RECS-TO-PROCESS

             ACCEPT PERSON-ID OF FILE2-AND-3-REC-LAYOUT-IN
             ACCEPT CITY-ID OF FILE2-AND-3-REC-LAYOUT-IN
             ACCEPT CITY-NAME OF FILE2-AND-3-REC-LAYOUT-IN
             ACCEPT STATE-NAME OF FILE2-AND-3-REC-LAYOUT-IN
             ADD 1 TO WS-NUM-INPUT-RECS

             MOVE CITY-ID OF FILE2-AND-3-REC-LAYOUT-IN
               TO CITY-ID OF FILE2-REC-LAYOUT-OUT
             MOVE PERSON-ID OF FILE2-AND-3-REC-LAYOUT-IN
               TO PERSON-ID OF FILE2-REC-LAYOUT-OUT
             MOVE CITY-NAME OF FILE2-AND-3-REC-LAYOUT-IN
               TO CITY-NAME OF FILE2-REC-LAYOUT-OUT
             MOVE STATE-NAME OF FILE2-AND-3-REC-LAYOUT-IN
               TO STATE-NAME OF FILE2-REC-LAYOUT-OUT

            WRITE FILE2-REC-LAYOUT-OUT
            IF WS-FILE2-STATUS NOT = '00'
              DISPLAY 'ERROR IN 000-FILE2-PARA'
              DISPLAY 'ERROR WRITING TO FILE2'
              DISPLAY 'CITY-ID: ' CITY-ID OF FILE2-AND-3-REC-LAYOUT-IN
              DISPLAY 'FILE STATUS: ' WS-FILE2-STATUS
              CALL WS-ABENDPGM
            END-IF
            ADD 1 TO WS-NUM-OUTPUT-RECS
           END-PERFORM.

       020-EXIT-PARA. EXIT.

       030-FILE3-PARA.
           ACCEPT WS-NUM-RECS-TO-PROCESS.
           PERFORM VARYING WS-I FROM 1 BY 1
             UNTIL WS-I > WS-NUM-RECS-TO-PROCESS

             ACCEPT PERSON-ID OF FILE2-AND-3-REC-LAYOUT-IN
             ACCEPT CITY-ID OF FILE2-AND-3-REC-LAYOUT-IN
             ACCEPT CITY-NAME OF FILE2-AND-3-REC-LAYOUT-IN
             ACCEPT STATE-NAME OF FILE2-AND-3-REC-LAYOUT-IN
             ADD 1 TO WS-NUM-INPUT-RECS

             MOVE CITY-ID OF FILE2-AND-3-REC-LAYOUT-IN
               TO CITY-ID OF FILE3-REC-LAYOUT-OUT
             MOVE PERSON-ID OF FILE2-AND-3-REC-LAYOUT-IN
               TO PERSON-ID OF FILE3-REC-LAYOUT-OUT
             MOVE CITY-NAME OF FILE2-AND-3-REC-LAYOUT-IN
               TO CITY-NAME OF FILE3-REC-LAYOUT-OUT
             MOVE STATE-NAME OF FILE2-AND-3-REC-LAYOUT-IN
               TO STATE-NAME OF FILE3-REC-LAYOUT-OUT

            WRITE FILE3-REC-LAYOUT-OUT
            IF WS-FILE3-STATUS NOT = '00'
              DISPLAY 'ERROR IN 000-FILE3-PARA'
              DISPLAY 'ERROR WRITING TO FILE3'
              DISPLAY 'CITY-ID: ' CITY-ID OF FILE2-AND-3-REC-LAYOUT-IN
              DISPLAY 'FILE STATUS: ' WS-FILE3-STATUS
              CALL WS-ABENDPGM
            END-IF
            ADD 1 TO WS-NUM-OUTPUT-RECS
           END-PERFORM.

       030-EXIT-PARA. EXIT.

