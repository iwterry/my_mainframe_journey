       ID DIVISION.
       PROGRAM-ID. CBLPRAC2.

      ************************ DESCRIPTION ******************** 
      *    - THIS COBOL PROGRAM IS USED TO PRACTICE MATCHING LOGIC
      *      WITH TWO INPUT PS DATA FILES.
      *********************************************************

       ENVIRONMENT DIVISION.
       INPUT-OUTPUT SECTION.
       FILE-CONTROL.
           SELECT FILE1 ASSIGN TO DDFILE1
           ORGANIZATION IS SEQUENTIAL
           ACCESS MODE IS SEQUENTIAL
           FILE STATUS IS WS-FILE1-STATUS.

           SELECT FILE2 ASSIGN TO DDFILE2
           ORGANIZATION IS SEQUENTIAL
           ACCESS MODE IS SEQUENTIAL
           FILE STATUS IS WS-FILE2-STATUS.

           SELECT FILE3 ASSIGN TO DDFILE3
           ORGANIZATION IS SEQUENTIAL
           ACCESS MODE IS SEQUENTIAL
           FILE STATUS IS WS-FILE3-STATUS.

       DATA DIVISION.
       FILE SECTION.
       FD FILE1 LABEL RECORD IS STANDARD.
       COPY FILE1CPY
         REPLACING FILE1-REC-LAYOUT BY FILE1-REC-LAYOUT-IN.

       FD FILE2 LABEL RECORD IS STANDARD.
       COPY FILE2CPY
         REPLACING FILE2-REC-LAYOUT BY FILE2-REC-LAYOUT-IN.

       FD FILE3 LABEL RECORD IS STANDARD.
       COPY FILE3CPY
         REPLACING FILE3-REC-LAYOUT BY FILE3-REC-LAYOUT-OUT.

       WORKING-STORAGE SECTION.
       01 WS-FILE1-STATUS PIC X(02).
       01 WS-FILE2-STATUS PIC X(02).
       01 WS-FILE3-STATUS PIC X(02).

       01 WS-NUM-INPUT-RECS PIC 9(02).
       01 WS-NUM-OUTPUT-RECS PIC 9(02).

       01 WS-ARE-MORE-RECS-FILE1 PIC X(01).
       01 WS-ARE-MORE-RECS-FILE2 PIC X(01).

       01 WS-ABENDPGM PIC X(08) VALUE 'ABENDPGM'.

       01 WS-PERSON-ID-NUM-F1-HOLD PIC 9(02) VALUE ZERO.
       01 WS-PERSON-ID-NUM-F2-HOLD PIC 9(02) VALUE ZERO.

       LINKAGE SECTION.

       PROCEDURE DIVISION.
       000-MAIN-PARA.
           DISPLAY 'CBLPRAC2 STARTED'.

           PERFORM 100-INIT-PARA THRU 100-EXIT-PARA.
           PERFORM 200-GET-REC-PARA THRU 200-EXIT-PARA.
           PERFORM 300-PROCESS-PARA THRU 300-EXIT-PARA
             UNTIL WS-ARE-MORE-RECS-FILE2 = 'N'.

           CLOSE FILE1 FILE2 FILE3.

           DISPLAY 'NUM INPUT RECORDS: ' WS-NUM-INPUT-RECS.
           DISPLAY 'NUM OUTPUT RECORDS: ' WS-NUM-OUTPUT-RECS.

           STOP RUN.


       100-INIT-PARA.
           MOVE 'Y' TO WS-ARE-MORE-RECS-FILE1
                       WS-ARE-MORE-RECS-FILE2.
           MOVE ZERO TO WS-NUM-INPUT-RECS
                        WS-NUM-OUTPUT-RECS.
           MOVE SPACE TO WS-FILE1-STATUS
                         WS-FILE2-STATUS
                         WS-FILE3-STATUS.

           OPEN INPUT FILE1 FILE2.
           OPEN OUTPUT FILE3.
           IF WS-FILE1-STATUS NOT = '00' OR
              WS-FILE2-STATUS NOT = '00' OR
              WS-FILE3-STATUS NOT = '00'

             DISPLAY 'ERROR IN 100-INIT-PARA'
             DISPLAY 'ERROR OPENING FILE(S)'
             DISPLAY 'FILE1 STATUS: ' WS-FILE1-STATUS
             DISPLAY 'FILE2 STATUS: ' WS-FILE2-STATUS
             DISPLAY 'FILE3 STATUS: ' WS-FILE3-STATUS
             CALL WS-ABENDPGM
           END-IF.

           INITIALIZE FILE1-REC-LAYOUT-IN
                      FILE2-REC-LAYOUT-IN
                      FILE3-REC-LAYOUT-OUT.

       100-EXIT-PARA. EXIT.


       200-GET-REC-PARA.
           PERFORM 210-GET-REC-FILE1-PARA THRU 210-EXIT-PARA.
           PERFORM 220-GET-REC-FILE2-PARA THRU 220-EXIT-PARA.

       200-EXIT-PARA. EXIT.


       210-GET-REC-FILE1-PARA.
           READ FILE1
             AT END
               MOVE 'N' TO WS-ARE-MORE-RECS-FILE1
            END-READ.

       210-EXIT-PARA. EXIT.


       220-GET-REC-FILE2-PARA.
           READ FILE2
             AT END
               MOVE 'N' TO WS-ARE-MORE-RECS-FILE2
               GO TO 220-EXIT-PARA
           END-READ.

           ADD 1 TO WS-NUM-INPUT-RECS.

       220-EXIT-PARA. EXIT.


       300-PROCESS-PARA.
           MOVE PERSON-ID OF FILE1-REC-LAYOUT-IN
             TO WS-PERSON-ID-NUM-F1-HOLD.
           MOVE PERSON-ID OF FILE2-REC-LAYOUT-IN
             TO WS-PERSON-ID-NUM-F2-HOLD.

           IF WS-PERSON-ID-NUM-F1-HOLD = WS-PERSON-ID-NUM-F2-HOLD
             PERFORM 310-MOVE-TO-FILE3-LAYOUT-PARA THRU 310-EXIT-PARA
             PERFORM 320-WRITE-FILE3-LAYOUT-PARA THRU 320-EXIT-PARA
             PERFORM 220-GET-REC-FILE2-PARA THRU 220-EXIT-PARA
           ELSE
           IF WS-PERSON-ID-NUM-F1-HOLD  < WS-PERSON-ID-NUM-F2-HOLD
             PERFORM 330-PROCESS-ID2-GT-ID1-PARA THRU 330-EXIT-PARA
           ELSE
             DISPLAY 'ERROR IN 300-PROCESS-PARA'
             PERFORM 340-PROCESS-ID-NOT-IN-F1-PARA THRU 340-EXIT-PARA
           END-IF
           END-IF.

       300-EXIT-PARA. EXIT.


       310-MOVE-TO-FILE3-LAYOUT-PARA.
           MOVE PERSON-ID OF FILE1-REC-LAYOUT-IN
             TO PERSON-ID OF FILE3-REC-LAYOUT-OUT.

           MOVE PERSON-NAME OF FILE1-REC-LAYOUT-IN
             TO PERSON-NAME OF FILE3-REC-LAYOUT-OUT.

           MOVE CITY-ID OF FILE2-REC-LAYOUT-IN
             TO CITY-ID IN FILE3-REC-LAYOUT-OUT.

           MOVE CITY-NAME OF FILE2-REC-LAYOUT-IN
             TO CITY-NAME IN FILE3-REC-LAYOUT-OUT.

           MOVE STATE-NAME OF FILE2-REC-LAYOUT-IN
             TO STATE-NAME IN FILE3-REC-LAYOUT-OUT.

       310-EXIT-PARA. EXIT.


       320-WRITE-FILE3-LAYOUT-PARA.
           WRITE FILE3-REC-LAYOUT-OUT.
           IF WS-FILE3-STATUS NOT = '00'
             DISPLAY 'ERROR IN 320-WRITE-FILE3-LAYOUT-PARA'
             DISPLAY 'ERROR WRITING TO FILE3'
             DISPLAY 'PERSON-ID: ' PERSON-ID OF FILE1-REC-LAYOUT-IN
             DISPLAY 'CITY-ID: ' CITY-ID OF FILE2-REC-LAYOUT-IN
             DISPLAY 'FILE STATUS: ' WS-FILE3-STATUS
             CALL WS-ABENDPGM
           END-IF.

           ADD 1 TO WS-NUM-OUTPUT-RECS.

       320-EXIT-PARA. EXIT.


      *    WHEN PERSON-ID IN FILE 2 IS GREATER THAN PERSON-ID IN FILE 1
       330-PROCESS-ID2-GT-ID1-PARA.
           IF WS-ARE-MORE-RECS-FILE1 = 'Y'
             PERFORM 210-GET-REC-FILE1-PARA THRU 210-EXIT-PARA
           ELSE
             DISPLAY 'ERROR IN 330-PROCESS-ID2-GT-ID1-PARA'
             PERFORM 340-PROCESS-ID-NOT-IN-F1-PARA THRU 340-EXIT-PARA
           END-IF.

       330-EXIT-PARA. EXIT.

 
       340-PROCESS-ID-NOT-IN-F1-PARA.
           DISPLAY 'PERSON-ID FOUND IN FILE2 BUT NOT IN FILE1'.
           DISPLAY 'PERSON-ID: ' PERSON-ID OF FILE2-REC-LAYOUT-IN.
           CALL WS-ABENDPGM.

       340-EXIT-PARA. EXIT.
