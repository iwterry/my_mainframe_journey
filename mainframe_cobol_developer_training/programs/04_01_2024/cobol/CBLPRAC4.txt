       ID DIVISION.
       PROGRAM-ID. CBLPRAC4.

      ************************ DESCRIPTION ******************** 
      *    - THIS COBOL PROGRAM IS USED TO PRACTICE LOOKUP LOGIC
      *      WITH ONE INPUT PS DATA FILE AND THE OTHER INPUT DATA 
      *      SOURCE COMING FROM DATA STORED IN A COPYBOOK.
      *********************************************************

       ENVIRONMENT DIVISION.
       INPUT-OUTPUT SECTION.
       FILE-CONTROL.
           SELECT FILE1 ASSIGN TO DDFILE1
           ORGANIZATION IS SEQUENTIAL
           ACCESS MODE IS SEQUENTIAL
           FILE STATUS IS WS-FILE1-STATUS.

           SELECT FILE2 ASSIGN TO DDFILE2
           ORGANIZATION IS SEQUENTIAL
           ACCESS MODE IS SEQUENTIAL
           FILE STATUS IS WS-FILE2-STATUS.

       DATA DIVISION.
       FILE SECTION.
       FD FILE1 LABEL RECORD IS STANDARD.
       COPY FILE2CPY
         REPLACING FILE2-REC-LAYOUT BY FILE1-REC-LAYOUT-IN.

       FD FILE2 LABEL RECORD IS STANDARD.
       COPY FILE3CPY
         REPLACING FILE3-REC-LAYOUT BY FILE2-REC-LAYOUT-OUT.

       WORKING-STORAGE SECTION.
       01 WS-FILE1-STATUS PIC X(02) VALUE SPACE.
       01 WS-FILE2-STATUS PIC X(02) VALUE SPACE.
       01 WS-ARE-MORE-RECS-ARRAY PIC X(01).
       01 WS-ARE-MORE-RECS-FILE1 PIC X(01).
       01 WS-NUM-INPUT-RECS PIC 9(02).
       01 WS-NUM-OUTPUT-RECS PIC 9(02).
       01 WS-ABENDPGM PIC X(08) VALUE 'ABENDPGM'.

       COPY PERSON.

       PROCEDURE DIVISION.
       000-MAIN-PARA.
           DISPLAY 'CBLPRAC4 STARTED'.

           PERFORM 100-INIT-PARA THRU 100-EXIT-PARA.
           PERFORM 200-GET-REC-PARA THRU 200-EXIT-PARA.
           PERFORM 300-PROCESS-PARA THRU 300-EXIT-PARA
              UNTIL WS-ARE-MORE-RECS-FILE1 = 'N'.

           CLOSE FILE1 FILE2.
           DISPLAY 'NUM INPUT RECORDS: ' WS-NUM-INPUT-RECS.
           DISPLAY 'NUM OUTPUT RECORDS: ' WS-NUM-OUTPUT-RECS.

           STOP RUN.


       100-INIT-PARA.
           MOVE 'Y' TO WS-ARE-MORE-RECS-FILE1
                       WS-ARE-MORE-RECS-FILE1.
           MOVE ZERO TO WS-NUM-INPUT-RECS
                         WS-NUM-OUTPUT-RECS.

           OPEN INPUT FILE1.
           OPEN OUTPUT FILE2.

           IF WS-FILE1-STATUS NOT = '00' OR WS-FILE2-STATUS NOT = '00'
              DISPLAY 'ERROR IN 100-INIT-PARA'
              DISPLAY 'ERROR OPENING FILE(S)'
              DISPLAY 'FILE1 STATUS: ' WS-FILE1-STATUS
              DISPLAY 'FILE2 STATUS: ' WS-FILE2-STATUS
              CALL WS-ABENDPGM
           END-IF.

           INITIALIZE FILE1-REC-LAYOUT-IN FILE2-REC-LAYOUT-OUT.

       100-EXIT-PARA. EXIT.


       200-GET-REC-PARA.
           READ FILE1
             AT END
               MOVE 'N' TO WS-ARE-MORE-RECS-FILE1
               GO TO 200-EXIT-PARA
           END-READ.

           ADD 1 TO WS-NUM-INPUT-RECS.

       200-EXIT-PARA. EXIT.


       300-PROCESS-PARA.
           SEARCH ALL PERSON-TABLE
             AT END
               DISPLAY 'ERROR IN 300-PROCESS-PARA'
               DISPLAY 'PERSON-ID NOT FOUND IN TABLE'
               DISPLAY 'PERSON-ID: ' PERSON-ID OF FILE1-REC-LAYOUT-IN
               CALL WS-ABENDPGM
             WHEN PERSON-ID OF PERSON-TABLE(INDX) =
               PERSON-ID IN FILE1-REC-LAYOUT-IN

               PERFORM 310-MOVE-TO-FILE2-LAYOUT-PARA THRU 310-EXIT-PARA
               PERFORM 320-WRITE-FILE2-LAYOUT-PARA THRU 320-EXIT-PARA
           END-SEARCH.

           PERFORM 200-GET-REC-PARA THRU 200-EXIT-PARA.

       300-EXIT-PARA. EXIT.


       310-MOVE-TO-FILE2-LAYOUT-PARA.
           MOVE PERSON-ID OF PERSON-TABLE(INDX)
             TO PERSON-ID IN FILE2-REC-LAYOUT-OUT.

           MOVE PERSON-NAME OF PERSON-TABLE(INDX)
             TO PERSON-NAME IN FILE2-REC-LAYOUT-OUT.

           MOVE CITY-ID OF FILE1-REC-LAYOUT-IN
             TO CITY-ID OF FILE2-REC-LAYOUT-OUT.

           MOVE CITY-NAME OF FILE1-REC-LAYOUT-IN
             TO CITY-NAME OF FILE2-REC-LAYOUT-OUT.

           MOVE STATE-NAME OF FILE1-REC-LAYOUT-IN
             TO STATE-NAME OF FILE2-REC-LAYOUT-OUT.

       310-EXIT-PARA. EXIT.


       320-WRITE-FILE2-LAYOUT-PARA.
           WRITE FILE2-REC-LAYOUT-OUT.

           IF WS-FILE2-STATUS NOT = '00'
              DISPLAY 'ERROR IN 320-WRITE-FILE2-LAYOUT-PARA'
              DISPLAY 'ERROR WRITING TO FILE2'
              DISPLAY 'CITY-ID: ' CITY-ID OF FILE1-REC-LAYOUT-IN
              DISPLAY 'PERSON-ID: ' PERSON-ID OF FILE1-REC-LAYOUT-IN
              DISPLAY 'FILE STATUS: ' WS-FILE2-STATUS
              CALL WS-ABENDPGM
           END-IF.

           ADD 1 TO WS-NUM-OUTPUT-RECS.

       320-EXIT-PARA. EXIT.
