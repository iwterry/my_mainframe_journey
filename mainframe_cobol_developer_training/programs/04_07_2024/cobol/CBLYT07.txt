       IDENTIFICATION DIVISION.
       PROGRAM-ID. CBLYT07.

       ENVIRONMENT DIVISION.
       INPUT-OUTPUT SECTION.
       FILE-CONTROL.
           SELECT MAST2-FILE-IN ASSIGN TO MASTER2
           ORGANIZATION IS SEQUENTIAL
           ACCESS MODE IS SEQUENTIAL
           FILE STATUS IS WS-MAST2-STATUS-IN.

           SELECT TRAN2-FILE-IN ASSIGN TO TRANSAC2
           ORGANIZATION IS SEQUENTIAL
           ACCESS MODE IS SEQUENTIAL
           FILE STATUS IS WS-TRAN2-STATUS-IN.

           SELECT BILL-FILE-OUT ASSIGN TO BILL
           ORGANIZATION IS SEQUENTIAL
           ACCESS MODE IS SEQUENTIAL
           FILE STATUS IS WS-BILL-STATUS-OUT.

       DATA DIVISION.
       FILE SECTION.
       FD MAST2-FILE-IN LABEL RECORD IS STANDARD.
       COPY MASTCPY REPLACING CC-LAYOUT BY MAST2-LAYOUT-IN.

       FD TRAN2-FILE-IN LABEL RECORD IS STANDARD.
       COPY TRTOTCPY REPLACING CC-TRAN-TOTAL-LAYOUT
                     BY TRAN2-LAYOUT-IN.

       FD BILL-FILE-OUT LABEL RECORD IS STANDARD.
       COPY BILLCPY REPLACING CC-BILL-LAYOUT BY BILL-LAYOUT-OUT.

       WORKING-STORAGE SECTION.
       01 WS-MAST2-STATUS-IN PIC X(02) VALUE SPACE.
       01 WS-TRAN2-STATUS-IN PIC X(02) VALUE SPACE.
       01 WS-BILL-STATUS-OUT PIC X(02) VALUE SPACE.

       01 WS-IN-REC-COUNT PIC 9(02) VALUE ZERO.
       01 WS-OUT-REC-COUNT PIC 9(02) VALUE ZERO.

       01 WS-ANY-MORE-INPUT-MAST PIC X(01) VALUE 'Y'.
       01 WS-ANY-MORE-INPUT-TRAN PIC X(01) VALUE 'Y'.

       01 WS-BILL-DATE-ALPHA-NUM PIC X(08) VALUE '20221024'.
       01 WS-BILL-DATE-NUM PIC 9(08) VALUE ZERO.
       01 WS-NUM-DAYS PIC 9(08) VALUE ZERO.
       01 WS-DUE-DATE-ALPHA-NUM PIC X(08) VALUE SPACE.
       01 WS-DUE-DATE-NUM PIC 9(08) VALUE ZERO.
       01 WS-NUM-DAYS-UNTIL-BILL-DUE PIC 9(02) VALUE 20.

       01 WS-TRAN-AMOUNT PIC S9(05)V9(02) VALUE ZERO.
       01 WS-INTEREST-PERC PIC 9(02) VALUE 15.
       01 WS-TOTAL-BILL-AMOUNT PIC S9(05)V9(02) VALUE ZERO.
       01 WS-MIN-AMOUNT-PERC PIC 9(02) VALUE 20.
       01 WS-MIN-PAYMENT-DUE PIC S9(05)V9(02) VALUE ZERO.

       01 WS-ABENDPGM PIC X(08) VALUE 'ABENDPGM'.

       PROCEDURE DIVISION.
       000-MAIN-PARA.
           DISPLAY 'CBLYT07 STARTED'.

           PERFORM 100-INITIAL-PARA THRU 100-EXIT-PARA.
           PERFORM 200-GET-INPUT-PARA THRU 200-EXIT-PARA.
           PERFORM 300-PROCESS-PARA THRU 300-EXIT-PARA
             UNTIL WS-ANY-MORE-INPUT-MAST = 'N'.

           IF WS-ANY-MORE-INPUT-TRAN = 'Y'
             PERFORM 100-INITIAL-PARA THRU 100-EXIT-PARA
             IF WS-ANY-MORE-INPUT-TRAN = 'Y'
               DISPLAY '100-MAIN-PARA'
               DISPLAY 'GIVEN CARD NUMBER NOT FOUND IN MAST FILE'
               DISPLAY 'CARD-NUMBER: ' CARD-NUMBER OF TRAN2-FILE-IN
               CALL WS-ABENDPGM
             END-IF
           END-IF.

           CLOSE MAST2-FILE-IN
                 TRAN2-FILE-IN
                 BILL-FILE-OUT.

           DISPLAY 'NUM OF INPUT RECORDS: ' WS-IN-REC-COUNT.
           DISPLAY 'NUM OF OUTPUT RECORDS: ' WS-OUT-REC-COUNT.

           STOP RUN.

       100-INITIAL-PARA.
           PERFORM 110-OPEN-INITIALIZE-PARA THRU 110-EXIT-PARA.
           PERFORM 120-CALC-BILL-DUE-DATE THRU 120-EXIT-PARA.

       100-EXIT-PARA. EXIT.

       110-OPEN-INITIALIZE-PARA.
           OPEN INPUT MAST2-FILE-IN TRAN2-FILE-IN.
           OPEN OUTPUT BILL-FILE-OUT.

           IF WS-MAST2-STATUS-IN NOT = '00' OR
              WS-TRAN2-STATUS-IN NOT = '00' OR
              WS-BILL-STATUS-OUT NOT = '00'

             DISPLAY 'ERROR IN 100-INITIAL-PARA'
             DISPLAY 'ERROR OPENING FILE(S)'
             DISPLAY 'MAST2-IN FILE STATUS: ' WS-MAST2-STATUS-IN
             DISPLAY 'TRAN2-IN FILE STATUS: ' WS-TRAN2-STATUS-IN
             DISPLAY 'BILL-OUT FILE STATUS: ' WS-BILL-STATUS-OUT
             CALL WS-ABENDPGM
           END-IF.

           INITIALIZE MAST2-LAYOUT-IN
                      TRAN2-LAYOUT-IN
                      BILL-LAYOUT-OUT.

       110-EXIT-PARA. EXIT.

       120-CALC-BILL-DUE-DATE.
           MOVE WS-BILL-DATE-ALPHA-NUM TO WS-BILL-DATE-NUM.

           COMPUTE WS-NUM-DAYS =
             FUNCTION INTEGER-OF-DATE(WS-BILL-DATE-NUM).

           ADD WS-NUM-DAYS-UNTIL-BILL-DUE TO WS-NUM-DAYS.

           COMPUTE WS-DUE-DATE-NUM =
             FUNCTION DATE-OF-INTEGER(WS-NUM-DAYS).

           MOVE WS-DUE-DATE-NUM TO WS-DUE-DATE-ALPHA-NUM.

       120-EXIT-PARA. EXIT.

       200-GET-INPUT-PARA.
           PERFORM 210-GET-INPUT-MAST-PARA THRU 210-EXIT-PARA.
           PERFORM 220-GET-INPUT-TRAN-PARA THRU 220-EXIT-PARA.

       200-EXIT-PARA. EXIT.

       210-GET-INPUT-MAST-PARA.
           READ MAST2-FILE-IN
             AT END
               MOVE 'N' TO WS-ANY-MORE-INPUT-MAST
               GO TO 210-EXIT-PARA
           END-READ.

           ADD 1 TO WS-IN-REC-COUNT.

       210-EXIT-PARA. EXIT.

       220-GET-INPUT-TRAN-PARA.
           READ TRAN2-FILE-IN
             AT END
               MOVE 'N' TO WS-ANY-MORE-INPUT-TRAN
           END-READ.

       220-EXIT-PARA. EXIT.

       300-PROCESS-PARA.

           IF CARD-NUMBER OF MAST2-LAYOUT-IN =
              CARD-NUMBER OF TRAN2-LAYOUT-IN

             MOVE TOTAL-TRANS-AMOUNT TO WS-TRAN-AMOUNT
             PERFORM 330-CALCULATE-BILL-PARA THRU 330-EXIT-PARA
             PERFORM 350-MOVE-WRITE-PARA THRU 350-EXIT-PARA
             PERFORM 200-GET-INPUT-PARA THRU 200-EXIT-PARA
           ELSE
           IF (CARD-NUMBER OF MAST2-LAYOUT-IN <
              CARD-NUMBER OF TRAN2-LAYOUT-IN) OR
              (WS-ANY-MORE-INPUT-TRAN = 'N')
             MOVE 0 TO WS-TRAN-AMOUNT
             PERFORM 330-CALCULATE-BILL-PARA THRU 330-EXIT-PARA

             IF WS-TOTAL-BILL-AMOUNT NOT = 0
               PERFORM 350-MOVE-WRITE-PARA THRU 350-EXIT-PARA
             END-IF

             PERFORM 210-GET-INPUT-MAST-PARA THRU 210-EXIT-PARA
           ELSE
             DISPLAY '300-PROCESS-PARA'
             DISPLAY 'GIVEN CARD NUMBER NOT FOUND IN MAST FILE'
             DISPLAY 'CARD-NUMBER: ' CARD-NUMBER OF TRAN2-FILE-IN
             CALL WS-ABENDPGM
           END-IF
           END-IF.

       300-EXIT-PARA. EXIT.


       330-CALCULATE-BILL-PARA.
           COMPUTE WS-TOTAL-BILL-AMOUNT =
            (PREVIOUS-DUE - PREVIOUS-DUE-PAID) *
            (1 + WS-INTEREST-PERC / 100) +
            WS-TRAN-AMOUNT.

            COMPUTE WS-MIN-PAYMENT-DUE =
              WS-TOTAL-BILL-AMOUNT * (WS-MIN-AMOUNT-PERC / 100).

       330-EXIT-PARA. EXIT.

       350-MOVE-WRITE-PARA.
           MOVE CARD-NUMBER OF MAST2-LAYOUT-IN
             TO CARD-NUMBER OF BILL-LAYOUT-OUT.

           MOVE CUST-NAME OF MAST2-LAYOUT-IN
             TO CUST-NAME OF BILL-LAYOUT-OUT.

           MOVE WS-TOTAL-BILL-AMOUNT
             TO TOTAL-AMOUNT OF BILL-LAYOUT-OUT.

           MOVE WS-MIN-PAYMENT-DUE
             TO MIN-DUE OF BILL-LAYOUT-OUT.

           MOVE WS-BILL-DATE-ALPHA-NUM
             TO BILL-DATE OF BILL-LAYOUT-OUT.

           MOVE WS-DUE-DATE-ALPHA-NUM
             TO DUE-DATE OF BILL-LAYOUT-OUT.

           WRITE BILL-LAYOUT-OUT.
           IF WS-BILL-STATUS-OUT NOT = '00'
             DISPLAY 'ERROR IN 350-MOVE-WRITE-PARA'
             DISPLAY 'WRITING FILE ERROR STATUS: ' WS-BILL-STATUS-OUT
             DISPLAY 'CARD-NUMBER: ' MAST2-LAYOUT-IN
             CALL WS-ABENDPGM
           END-IF.

           ADD 1 TO WS-OUT-REC-COUNT.

       350-EXIT-PARA. EXIT.