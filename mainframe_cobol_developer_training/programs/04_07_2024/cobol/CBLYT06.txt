       IDENTIFICATION DIVISION.
       PROGRAM-ID. CBLYT06.

       ENVIRONMENT DIVISION.
       INPUT-OUTPUT SECTION.
       FILE-CONTROL.
           SELECT TRAN1-FILE-IN ASSIGN TO TRANSAC1
           ORGANIZATION IS SEQUENTIAL
           ACCESS MODE IS SEQUENTIAL
           FILE STATUS IS WS-TRAN1-STATUS-IN.

           SELECT TRAN2-FILE-OUT ASSIGN TO TRANSAC2
           ORGANIZATION IS SEQUENTIAL
           ACCESS MODE IS SEQUENTIAL
           FILE STATUS IS WS-TRAN2-STATUS-OUT.

       DATA DIVISION.
       FILE SECTION.
       FD TRAN1-FILE-IN LABEL RECORD IS STANDARD.
       COPY TRANCPY REPLACING CC-TRAN-LAYOUT BY TRANS1-LAYOUT-IN.

       FD TRAN2-FILE-OUT LABEL RECORD IS STANDARD.
       COPY TRTOTCPY REPLACING CC-TRAN-TOTAL-LAYOUT
                     BY TRAN2-LAYOUT-OUT.


       WORKING-STORAGE SECTION.
       01 WS-TRAN1-STATUS-IN PIC X(02) VALUE SPACE.
       01 WS-TRAN2-STATUS-OUT PIC X(02) VALUE SPACE.
       01 WS-IN-REC-COUNT PIC 9(02) VALUE ZERO.
       01 WS-OUT-REC-COUNT PIC 9(02) VALUE ZERO.
       01 WS-ANY-MORE-INPUT PIC X(01) VALUE 'Y'.
       01 WS-TOTAL-TRANS-AMOUNT PIC S9(05)V9(02) VALUE ZERO.
       01 WS-PREV-CARD-NUMBER PIC X(05) VALUE SPACE.

       01 WS-ABENDPGM PIC X(08) VALUE 'ABENDPGM'.

       PROCEDURE DIVISION.
       000-MAIN-PARA.
           DISPLAY 'CBLYT06 STARTED'.

           PERFORM 100-INITIAL-PARA THRU 100-EXIT-PARA.
           PERFORM 200-GET-INPUT-PARA THRU 200-EXIT-PARA.
           PERFORM 300-PROCESS-PARA THRU 300-EXIT-PARA
             UNTIL WS-ANY-MORE-INPUT = 'N'.

           CLOSE TRAN1-FILE-IN TRAN2-FILE-OUT.
           DISPLAY 'INPUT RECORDS COUNT: ' WS-IN-REC-COUNT.
           DISPLAY 'OUTPUT RECORDS COUNT :' WS-OUT-REC-COUNT.

           STOP RUN.

       100-INITIAL-PARA.
           OPEN INPUT TRAN1-FILE-IN.
           OPEN OUTPUT TRAN2-FILE-OUT.

           IF WS-TRAN1-STATUS-IN NOT = '00' OR
              WS-TRAN2-STATUS-OUT NOT = '00'

              DISPLAY 'ERROR IN 100-INITIAL-PARA'
              DISPLAY 'ERROR OPENING FILE(S)'
              DISPLAY 'TRAN1-IN STATUS: ' WS-TRAN1-STATUS-IN
              DISPLAY 'TRAN2-OUT STATUS: ' WS-TRAN2-STATUS-OUT
              CALL WS-ABENDPGM
           END-IF.
           INITIALIZE TRANS1-LAYOUT-IN TRAN2-LAYOUT-OUT.

       100-EXIT-PARA. EXIT.

       200-GET-INPUT-PARA.
           READ TRAN1-FILE-IN
             AT END
               MOVE 'N' TO WS-ANY-MORE-INPUT
               GO TO 200-EXIT-PARA
           END-READ.
           ADD 1 TO WS-IN-REC-COUNT.

       200-EXIT-PARA. EXIT.

       300-PROCESS-PARA.
           MOVE CARD-NUMBER OF TRANS1-LAYOUT-IN
             TO WS-PREV-CARD-NUMBER.
           ADD TRANS-AMOUNT OF TRANS1-LAYOUT-IN
             TO WS-TOTAL-TRANS-AMOUNT.

           PERFORM 200-GET-INPUT-PARA THRU 200-EXIT-PARA.

           IF WS-ANY-MORE-INPUT = 'N'
              PERFORM 350-MOVE-WRITE-PARA THRU 350-EXIT-PARA
           ELSE
           IF CARD-NUMBER OF TRAN1-FILE-IN = WS-PREV-CARD-NUMBER
              CONTINUE
           ELSE
              PERFORM 350-MOVE-WRITE-PARA THRU 350-EXIT-PARA
              MOVE 0 TO WS-TOTAL-TRANS-AMOUNT
           END-IF
           END-IF.

       300-EXIT-PARA. EXIT.

       350-MOVE-WRITE-PARA.
           MOVE WS-PREV-CARD-NUMBER
             TO CARD-NUMBER OF TRAN2-LAYOUT-OUT.

           MOVE WS-TOTAL-TRANS-AMOUNT
             TO TOTAL-TRANS-AMOUNT.

           WRITE TRAN2-LAYOUT-OUT.
           IF WS-TRAN2-STATUS-OUT NOT = '00'
             DISPLAY '350-MOVE-WRITE-PARA'
             DISPLAY 'ERROR WRITING FILE STATUS: ' WS-TRAN2-STATUS-OUT
             DISPLAY 'CARD NUMBER: ' CARD-NUMBER OF TRANS1-LAYOUT-IN
             CALL WS-ABENDPGM
           END-IF.

           ADD 1 TO WS-OUT-REC-COUNT.

       350-EXIT-PARA. EXIT.