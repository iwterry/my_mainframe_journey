       IDENTIFICATION DIVISION.
       PROGRAM-ID. CBLYT08.
       AUTHOR. NAME.
       DATE-WRITTEN. TODAY.

       ENVIRONMENT DIVISION.
       INPUT-OUTPUT SECTION.
       FILE-CONTROL.
           SELECT BILL-FILE-IN ASSIGN TO BILL
           ORGANIZATION IS SEQUENTIAL
           ACCESS MODE IS SEQUENTIAL
           FILE STATUS IS WS-BILL-STATUS-IN.

           SELECT MASTER2-FILE-IN ASSIGN TO MASTER2
           ORGANIZATION IS SEQUENTIAL
           ACCESS MODE IS SEQUENTIAL
           FILE STATUS IS WS-MASTER2-STATUS-IN.

           SELECT MASTER3-FILE-OUT ASSIGN TO MASTER3
           ORGANIZATION IS SEQUENTIAL
           ACCESS MODE IS SEQUENTIAL
           FILE STATUS IS WS-MASTER3-STATUS-OUT.

       DATA DIVISION.
       FILE SECTION.
       FD BILL-FILE-IN LABEL RECORD IS STANDARD.
       COPY BILLCPY REPLACING CC-BILL-LAYOUT BY BILL-LAYOUT-IN.

       FD MASTER2-FILE-IN LABEL RECORD IS STANDARD.
       COPY MASTCPY REPLACING CC-LAYOUT BY MASTER2-LAYOUT-IN.

       FD MASTER3-FILE-OUT LABEL RECORD IS STANDARD.
       COPY MASTCPY REPLACING CC-LAYOUT BY MASTER3-LAYOUT-OUT.

       WORKING-STORAGE SECTION.
       01 WS-BILL-STATUS-IN PIC X(02) VALUE SPACE.
       01 WS-MASTER2-STATUS-IN PIC X(02) VALUE SPACE.
       01 WS-MASTER3-STATUS-OUT PIC X(02) VALUE SPACE.

       01 WS-IN-REC-COUNT PIC 9(02) VALUE ZERO.
       01 WS-OUT-REC-COUNT PIC 9(02) VALUE ZERO.

       01 WS-ANY-MORE-INPUT-MASTER2 PIC X(01) VALUE 'Y'.
       01 WS-ANY-MORE-INPUT-BILL PIC X(01) VALUE 'Y'.

       01 WS-TOTAL-DUE PIC S9(05)V9(02).
       01 WS-TOTAL-DUE-PAID PIC S9(05)V9(02).
       01 WS-BILL-DATE PIC X(08) VALUE SPACE.

       01 WS-ABENDPGM PIC X(08) VALUE 'ABENDPGM'.

       PROCEDURE DIVISION.
       000-MAIN-PARA.
           DISPLAY 'CBLYT08 STARTED'.

           PERFORM 100-INITIAL-PARA THRU 100-EXIT-PARA.
           PERFORM 200-GET-INPUT-PARA THRU 200-EXIT-PARA.
           PERFORM 300-PROCESS-PARA THRU 300-EXIT-PARA
             UNTIL WS-ANY-MORE-INPUT-MASTER2 = 'N'.

           IF WS-ANY-MORE-INPUT-BILL = 'Y'
             DISPLAY 'ERROR IN 000-MAIN-PARA'
             DISPLAY 'CARD NUMBER ' CARD-NUMBER OF BILL-LAYOUT-IN 
               ' NOT FOUND IN MASTER2-IN'
             CALL WS-ABENDPGM
           END-IF.

           CLOSE BILL-FILE-IN
                 MASTER2-FILE-IN
                 MASTER3-FILE-OUT.

           DISPLAY 'NUM OF INPUT RECORDS: ' WS-IN-REC-COUNT.
           DISPLAY 'NUM OF OUTPUT RECORDS: ' WS-OUT-REC-COUNT.

           STOP RUN.

       100-INITIAL-PARA.
           OPEN INPUT BILL-FILE-IN MASTER2-FILE-IN.
           OPEN OUTPUT MASTER3-FILE-OUT.

           IF WS-BILL-STATUS-IN NOT = '00' OR
              WS-MASTER2-STATUS-IN NOT = '00' OR
              WS-MASTER3-STATUS-OUT NOT = '00'

             DISPLAY 'ERROR IN 100-INITIAL PARA'
             DISPLAY 'ERROR OPENING FILE(S)'
             DISPLAY 'BILL-IN ERROR STATUS: ' WS-BILL-STATUS-IN
             DISPLAY 'MASTER2-IN ERROR STATUS: ' WS-MASTER2-STATUS-IN
             DISPLAY 'MASTER3-OUT ERROR STATUS: ' WS-MASTER3-STATUS-OUT
             CALL WS-ABENDPGM
           END-IF.

           INITIALIZE BILL-LAYOUT-IN
                      MASTER2-LAYOUT-IN
                      MASTER3-LAYOUT-OUT.

       100-EXIT-PARA. EXIT.

       200-GET-INPUT-PARA.
           PERFORM 210-GET-INPUT-BILL-PARA THRU 210-EXIT-PARA.
           PERFORM 220-GET-INPUT-MASTER2-PARA THRU 220-EXIT-PARA.

       200-EXIT-PARA. EXIT.

       210-GET-INPUT-BILL-PARA.
           READ BILL-FILE-IN
             AT END
               MOVE 'N' TO WS-ANY-MORE-INPUT-BILL
           END-READ.

       210-EXIT-PARA. EXIT.

       220-GET-INPUT-MASTER2-PARA.
           READ MASTER2-FILE-IN
             AT END
               MOVE 'N' TO WS-ANY-MORE-INPUT-MASTER2
               GO TO 220-EXIT-PARA
           END-READ.

           ADD 1 TO WS-IN-REC-COUNT.

       220-EXIT-PARA. EXIT.

       300-PROCESS-PARA.
           IF CARD-NUMBER OF MASTER2-LAYOUT-IN =
              CARD-NUMBER OF BILL-LAYOUT-IN
              
             MOVE TOTAL-AMOUNT OF BILL-LAYOUT-IN TO WS-TOTAL-DUE
             MOVE 0 TO WS-TOTAL-DUE-PAID
             MOVE BILL-DATE OF BILL-LAYOUT-IN TO WS-BILL-DATE

             PERFORM 350-MOVE-WRITE-PARA THRU 350-EXIT-PARA
             PERFORM 200-GET-INPUT-PARA THRU 200-EXIT-PARA
           ELSE
           IF (CARD-NUMBER OF MASTER2-LAYOUT-IN <
               CARD-NUMBER OF BILL-LAYOUT-IN) OR
              WS-ANY-MORE-INPUT-BILL = 'N'
             
             MOVE PREVIOUS-DUE IN MASTER2-LAYOUT-IN TO WS-TOTAL-DUE
             MOVE PREVIOUS-DUE-PAID IN MASTER2-LAYOUT-IN
               TO WS-TOTAL-DUE-PAID
             MOVE LAST-BILL-DATE IN MASTER2-LAYOUT-IN TO WS-BILL-DATE

             PERFORM 350-MOVE-WRITE-PARA THRU 350-EXIT-PARA
             PERFORM 220-GET-INPUT-MASTER2-PARA THRU 220-EXIT-PARA
           ELSE
             DISPLAY 'ERROR IN 300-PROCESS-PARA'
             DISPLAY 'CARD NUMBER ' CARD-NUMBER OF BILL-LAYOUT-IN 
              ' NOT FOUND IN MASTER2-IN'
             CALL WS-ABENDPGM
           END-IF
           END-IF.

       300-EXIT-PARA. EXIT.

       350-MOVE-WRITE-PARA.
           MOVE CARD-NUMBER OF MASTER2-LAYOUT-IN
             TO CARD-NUMBER OF MASTER3-LAYOUT-OUT.
           MOVE CUST-NAME OF MASTER2-LAYOUT-IN
             TO CUST-NAME OF MASTER3-LAYOUT-OUT.
           MOVE CARD-LIMIT OF MASTER2-LAYOUT-IN
             TO CARD-LIMIT OF MASTER3-LAYOUT-OUT.
           MOVE WS-TOTAL-DUE 
             TO PREVIOUS-DUE OF MASTER3-LAYOUT-OUT.
           MOVE WS-TOTAL-DUE-PAID
             TO PREVIOUS-DUE-PAID OF MASTER3-LAYOUT-OUT.
           MOVE CARD-TYPE OF MASTER2-LAYOUT-IN
             TO CARD-TYPE OF MASTER3-LAYOUT-OUT.
           MOVE REWARD-POINT-CODE OF MASTER2-LAYOUT-IN
             TO REWARD-POINT-CODE OF MASTER3-LAYOUT-OUT.
           MOVE CARD-EXPIRE-DATE OF MASTER2-LAYOUT-IN
             TO CARD-EXPIRE-DATE OF MASTER3-LAYOUT-OUT.
           MOVE CUST-ACTIVE-IND OF MASTER2-LAYOUT-IN
             TO CUST-ACTIVE-IND OF MASTER3-LAYOUT-OUT.
           MOVE WS-BILL-DATE
             TO LAST-BILL-DATE OF MASTER3-LAYOUT-OUT.


           WRITE MASTER3-LAYOUT-OUT.
           IF WS-MASTER3-STATUS-OUT NOT = '00'
             DISPLAY 'ERROR IN 350-MOVE-WRITE-PARA'
             DISPLAY 'WRITING FILE ERROR STATUS: ' WS-MASTER3-STATUS-OUT
             DISPLAY 'CARD-NUMBER: ' CARD-NUMBER OF MASTER2-LAYOUT-IN
             CALL WS-ABENDPGM
           END-IF.

           ADD 1 TO WS-OUT-REC-COUNT.
           
       350-EXIT-PARA.