       ID DIVISION.
       PROGRAM-ID. CBLYT01.

       ENVIRONMENT DIVISION.
       INPUT-OUTPUT SECTION.
       FILE-CONTROL.
           SELECT FILE1 ASSIGN TO FILE1
           ORGANIZATION IS SEQUENTIAL
           ACCESS MODE IS SEQUENTIAL
           FILE STATUS IS WS-FILE1-STATUS-OUT.

       DATA DIVISION.
       FILE SECTION.
       FD FILE1 LABEL RECORD IS STANDARD.
       COPY MASTCPY REPLACING CC-LAYOUT BY FILE1-LAYOUT-OUT.

       WORKING-STORAGE SECTION.
       COPY MASTCPY REPLACING CC-LAYOUT BY FILE1-LAYOUT-IN.

       01 WS-FILE1-STATUS-OUT PIC X(02) VALUE SPACE.
       01 WS-IN-REC-COUNT PIC 9(02) VALUE ZERO.
       01 WS-OUT-REC-COUNT PIC 9(02) VALUE ZERO.
       01 WS-ANY-MORE-INPUT PIC X(01) VALUE 'Y'.

       01 WS-ABENDPGM PIC X(08) VALUE 'ABENDPGM'.

       PROCEDURE DIVISION.
       000-MAIN-PARA.
           DISPLAY 'CBLYT01 STARTED'.

           PERFORM 100-INITIAL-PARA THRU 100-EXIT-PARA.
           PERFORM 200-GET-INPUT-PARA THRU 200-EXIT-PARA.
           PERFORM 300-PROCESS-PARA THRU 300-EXIT-PARA
             UNTIL WS-ANY-MORE-INPUT = 'N'.

           CLOSE FILE1.

           DISPLAY 'TOTAL INPUT RECORDS PROCESSED: ' WS-IN-REC-COUNT.
           DISPLAY 'TOTAL OUTPUT RECORDS PROCESSED: ' WS-OUT-REC-COUNT.

           STOP RUN.

       100-INITIAL-PARA.
           OPEN OUTPUT FILE1.
           IF WS-FILE1-STATUS-OUT NOT = '00'
             DISPLAY 'ERROR IN 100-INITIAL-PARA'
             DISPLAY 'ERROR OPENING FILE'
             DISPLAY 'FILE STATUS: ' WS-FILE1-STATUS-OUT
             CALL WS-ABENDPGM
           END-IF.

           INITIALIZE FILE1-LAYOUT-IN FILE1-LAYOUT-OUT.

       100-EXIT-PARA. EXIT.

       200-GET-INPUT-PARA.
           ACCEPT WS-ANY-MORE-INPUT.

           IF WS-ANY-MORE-INPUT = 'N'
              GO TO 200-EXIT-PARA
           END-IF.

           ACCEPT CARD-NUMBER OF FILE1-LAYOUT-IN.
           ACCEPT CUST-NAME OF FILE1-LAYOUT-IN.
           ACCEPT CARD-LIMIT OF FILE1-LAYOUT-IN.
           ACCEPT PREVIOUS-DUE OF FILE1-LAYOUT-IN.
           ACCEPT PREVIOUS-DUE-PAID OF FILE1-LAYOUT-IN.
           ACCEPT CARD-TYPE OF FILE1-LAYOUT-IN.
           ACCEPT REWARD-POINT-CODE OF FILE1-LAYOUT-IN.
           ACCEPT CARD-EXPIRE-DATE OF FILE1-LAYOUT-IN.
           ACCEPT CUST-ACTIVE-IND OF FILE1-LAYOUT-IN.
           ACCEPT LAST-BILL-DATE OF FILE1-LAYOUT-IN.

           ADD 1 TO WS-IN-REC-COUNT.

       200-EXIT-PARA. EXIT.

       300-PROCESS-PARA.
           PERFORM 350-MOVE-WRITE-PARA THRU 350-EXIT-PARA.
           PERFORM 200-GET-INPUT-PARA THRU 200-EXIT-PARA.

       300-EXIT-PARA. EXIT.

       350-MOVE-WRITE-PARA.
           MOVE CARD-NUMBER OF FILE1-LAYOUT-IN
             TO CARD-NUMBER OF FILE1-LAYOUT-OUT.
           MOVE CUST-NAME OF FILE1-LAYOUT-IN
             TO CUST-NAME OF FILE1-LAYOUT-OUT.
           MOVE CARD-LIMIT OF FILE1-LAYOUT-IN
             TO CARD-LIMIT OF FILE1-LAYOUT-OUT.
           MOVE PREVIOUS-DUE OF FILE1-LAYOUT-IN
             TO PREVIOUS-DUE OF FILE1-LAYOUT-OUT.
           MOVE PREVIOUS-DUE-PAID OF FILE1-LAYOUT-IN
             TO PREVIOUS-DUE-PAID OF FILE1-LAYOUT-OUT.
           MOVE CARD-TYPE OF FILE1-LAYOUT-IN
             TO CARD-TYPE OF FILE1-LAYOUT-OUT.
           MOVE REWARD-POINT-CODE OF FILE1-LAYOUT-IN
             TO REWARD-POINT-CODE  OF FILE1-LAYOUT-OUT.
           MOVE CARD-EXPIRE-DATE OF FILE1-LAYOUT-IN
             TO CARD-EXPIRE-DATE OF FILE1-LAYOUT-OUT.
           MOVE CUST-ACTIVE-IND OF FILE1-LAYOUT-IN
             TO CUST-ACTIVE-IND OF FILE1-LAYOUT-OUT.
           MOVE LAST-BILL-DATE OF FILE1-LAYOUT-IN
             TO LAST-BILL-DATE OF FILE1-LAYOUT-OUT.

           WRITE FILE1-LAYOUT-OUT.

           IF WS-FILE1-STATUS-OUT NOT = '00'
             DISPLAY 'ERROR IN 350-MOVE-WRITE-PARA'
             DISPLAY 'ERROR WRITING TO FILE'
             DISPLAY 'CARD NUMBER: ' 
               CARD-NUMBER OF FILE1-LAYOUT-IN
             DISPLAY 'FILE STATUS: ' WS-FILE1-STATUS-OUT
             CALL WS-ABENDPGM
           END-IF.

           ADD 1 TO WS-OUT-REC-COUNT.

       350-EXIT-PARA. EXIT.