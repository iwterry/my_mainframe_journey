       IDENTIFICATION DIVISION.
       PROGRAM-ID. CBLYT03.

       ENVIRONMENT DIVISION.
       INPUT-OUTPUT SECTION.
       FILE-CONTROL.
           SELECT MASTER1-FILE-IN ASSIGN TO MASTER1
           ORGANIZATION IS SEQUENTIAL
           ACCESS MODE IS SEQUENTIAL
           FILE STATUS IS WS-MASTER1-STATUS-IN.

           SELECT MASTER2-FILE-OUT ASSIGN TO MASTER2
           ORGANIZATION IS SEQUENTIAL
           ACCESS MODE IS SEQUENTIAL
           FILE STATUS IS WS-MASTER2-STATUS-OUT.

       DATA DIVISION.
       FILE SECTION.
       FD MASTER1-FILE-IN LABEL RECORD IS STANDARD.
       COPY MASTCPY REPLACING CC-LAYOUT BY MASTER1-LAYOUT-IN.

       FD MASTER2-FILE-OUT LABEL RECORD IS STANDARD.
       COPY MASTCPY REPLACING CC-LAYOUT BY MASTER2-LAYOUT-OUT.

       WORKING-STORAGE SECTION.
       01 WS-MASTER1-STATUS-IN PIC X(02) VALUE SPACE.
       01 WS-MASTER2-STATUS-OUT PIC X(02) VALUE SPACE.
       01 WS-IN-REC-COUNT PIC 9(02) VALUE ZERO.
       01 WS-OUT-REC-COUNT PIC 9(02) VALUE ZERO.
       01 WS-ANY-MORE-INPUT PIC X(01) VALUE 'Y'.
       01 WS-IS-CUSTOMER-ACTIVE PIC X(01) VALUE 'Y'.
       01 WS-LAST-BILL-DATE-NUM-HOLD PIC 9(08) VALUE ZERO.
       01 WS-DATE2-NUM-HOLD PIC 9(08) VALUE 20221009.

       01 WS-ABENDPGM PIC X(08) VALUE 'ABENDPGM'.

       PROCEDURE DIVISION.
       000-MAIN-PARA.
           DISPLAY 'CLBYT03 STARTED'.

           PERFORM 100-INITIAL-PARA THRU 100-EXIT-PARA.
           PERFORM 200-GET-INPUT-PARA THRU 200-EXIT-PARA.
           PERFORM 300-PROCESS-PARA THRU 300-EXIT-PARA
              UNTIL WS-ANY-MORE-INPUT = 'N'.

           CLOSE MASTER1-FILE-IN MASTER2-FILE-OUT.
           DISPLAY 'NUMBER OF INPUT RECORDS: ' WS-IN-REC-COUNT.
           DISPLAY 'NUMBER OF OUTPUT RECORDS: ' WS-OUT-REC-COUNT.

           STOP RUN.

       100-INITIAL-PARA.
           COMPUTE WS-DATE2-NUM-HOLD =
             FUNCTION INTEGER-OF-DATE(WS-DATE2-NUM-HOLD).

           OPEN INPUT MASTER1-FILE-IN.
           OPEN OUTPUT MASTER2-FILE-OUT.

           IF WS-MASTER1-STATUS-IN NOT = '00' OR
              WS-MASTER2-STATUS-OUT NOT = '00'

             DISPLAY 'ERROR IN 100-INITIAL-PARA'
             DISPLAY 'ERROR OPENING FILE(S)'
             DISPLAY 'MASTER1 FILE STATUS: ' WS-MASTER1-STATUS-IN
             DISPLAY 'MASTER2 FILE STATUS: ' WS-MASTER2-STATUS-OUT
             CALL WS-ABENDPGM
           END-IF.

           INITIALIZE MASTER1-LAYOUT-IN MASTER2-LAYOUT-OUT.

       100-EXIT-PARA. EXIT.

       200-GET-INPUT-PARA.
           READ MASTER1-FILE-IN
             AT END
               MOVE 'N' TO WS-ANY-MORE-INPUT
               GO TO 200-EXIT-PARA
           END-READ.

           ADD 1 TO WS-IN-REC-COUNT.

       200-EXIT-PARA. EXIT.

       300-PROCESS-PARA.
           PERFORM 320-SET-CUST-ACT-PARA THRU 320-EXIT-PARA.
           PERFORM 350-MOVE-WRITE-PARA THRU 350-EXIT-PARA.
           PERFORM 200-GET-INPUT-PARA THRU 200-EXIT-PARA.

       300-EXIT-PARA. EXIT.

       320-SET-CUST-ACT-PARA.
           MOVE LAST-BILL-DATE OF MASTER1-LAYOUT-IN
             TO WS-LAST-BILL-DATE-NUM-HOLD.
           COMPUTE WS-LAST-BILL-DATE-NUM-HOLD =
             FUNCTION INTEGER-OF-DATE(WS-LAST-BILL-DATE-NUM-HOLD).

           IF WS-LAST-BILL-DATE-NUM-HOLD >= WS-DATE2-NUM-HOLD - 60
             MOVE 'A' TO CUST-ACTIVE-IND  OF MASTER1-LAYOUT-IN
           ELSE
             MOVE 'I' TO CUST-ACTIVE-IND OF MASTER1-LAYOUT-IN
           END-IF.

       320-EXIT-PARA. EXIT.


       350-MOVE-WRITE-PARA.
           MOVE CARD-NUMBER OF MASTER1-LAYOUT-IN
             TO CARD-NUMBER OF MASTER2-LAYOUT-OUT.
           MOVE CUST-NAME OF MASTER1-LAYOUT-IN
             TO CUST-NAME OF MASTER2-LAYOUT-OUT.
           MOVE CARD-LIMIT OF MASTER1-LAYOUT-IN
             TO CARD-LIMIT OF MASTER2-LAYOUT-OUT.
           MOVE PREVIOUS-DUE OF MASTER1-LAYOUT-IN
             TO PREVIOUS-DUE OF MASTER2-LAYOUT-OUT.
           MOVE PREVIOUS-DUE-PAID OF MASTER1-LAYOUT-IN
             TO PREVIOUS-DUE-PAID OF MASTER2-LAYOUT-OUT.
           MOVE CARD-TYPE OF MASTER1-LAYOUT-IN
             TO CARD-TYPE OF MASTER2-LAYOUT-OUT.
           MOVE REWARD-POINT-CODE OF MASTER1-LAYOUT-IN
             TO REWARD-POINT-CODE  OF MASTER2-LAYOUT-OUT.
           MOVE CARD-EXPIRE-DATE OF MASTER1-LAYOUT-IN
             TO CARD-EXPIRE-DATE OF MASTER2-LAYOUT-OUT.
           MOVE CUST-ACTIVE-IND OF MASTER1-LAYOUT-IN
             TO CUST-ACTIVE-IND OF MASTER2-LAYOUT-OUT.
           MOVE LAST-BILL-DATE OF MASTER1-LAYOUT-IN
             TO LAST-BILL-DATE OF MASTER2-LAYOUT-OUT.

           WRITE MASTER2-LAYOUT-OUT.
           IF WS-MASTER2-STATUS-OUT NOT = '00'
              DISPLAY 'ERROR IN 350-MOVE-WRITE-PARA'
              DISPLAY 'ERROR WRITING TO FILE'
              DISPLAY 'CARD NUMBER: ' CARD-NUMBER
                OF MASTER1-LAYOUT-IN
              DISPLAY 'FILE STATUS: ' WS-MASTER2-STATUS-OUT
              CALL WS-ABENDPGM
           END-IF.

           ADD 1 TO WS-OUT-REC-COUNT.


       350-EXIT-PARA. EXIT.