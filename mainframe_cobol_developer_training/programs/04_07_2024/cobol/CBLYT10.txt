       IDENTIFICATION DIVISION.
       PROGRAM-ID. CBLYT10.

       ENVIRONMENT DIVISION.
       INPUT-OUTPUT SECTION.
       FILE-CONTROL.
           SELECT FILE3-IN ASSIGN TO FILE3
           ORGANIZATION IS SEQUENTIAL
           ACCESS MODE IS SEQUENTIAL
           FILE STATUS IS WS-FILE3-STATUS-IN.

           SELECT PAYMENT-FILE-OUT ASSIGN TO PAYMENT1
           ORGANIZATION IS SEQUENTIAL
           ACCESS MODE IS SEQUENTIAL
           FILE STATUS IS WS-PAYMENT-STATUS-OUT.

           SELECT FILE3-ERR-OUT ASSIGN TO FILE3ERR
           ORGANIZATION IS SEQUENTIAL
           ACCESS MODE IS SEQUENTIAL
           FILE STATUS IS WS-FILE3-ERR-STATUS-OUT.

       DATA DIVISION.
       FILE SECTION.
       FD FILE3-IN LABEL RECORD IS STANDARD.
       COPY PAYMTCPY REPLACING CC-PAYMENT-LAYOUT BY FILE3-LAYOUT-IN.

       FD PAYMENT-FILE-OUT LABEL RECORD IS STANDARD.
       COPY PAYMTCPY REPLACING CC-PAYMENT-LAYOUT BY PAYMENT-LAYOUT-OUT.

       FD FILE3-ERR-OUT LABEL RECORD IS STANDARD.
       COPY PAYMTCPY REPLACING CC-PAYMENT-LAYOUT 
                     BY FILE3-ERR-LAYOUT-OUT.

       WORKING-STORAGE SECTION.
       01 WS-PAYMENT-STATUS-OUT PIC X(02) VALUE SPACE.
       01 WS-FILE3-STATUS-IN PIC X(02) VALUE SPACE.
       01 WS-FILE3-ERR-STATUS-OUT PIC X(02) VALUE SPACE.

       01 WS-IN-REC-COUNT PIC 9(02) VALUE ZERO.
       01 WS-OUT-REC-COUNT PIC 9(02) VALUE ZERO.
       01 WS-ERR-REC-COUNT PIC 9(02) VALUE ZERO.

       01 WS-ANY-MORE-INPUT PIC X(01) VALUE 'Y'.
       01 WS-HAS-FIELD-ERROR PIC X(01) VALUE 'Y'.

       01 WS-ABENDPGM PIC X(08) VALUE 'ABENDPGM'.

       PROCEDURE DIVISION.
       000-MAIN-PARA.
           DISPLAY 'CBLYT10 STARTED'.
           PERFORM 100-INITIAL-PARA THRU 100-EXIT-PARA.
           PERFORM 200-GET-INPUT-PARA THRU 200-EXIT-PARA.
           PERFORM 300-PROCESS-PARA THRU 300-EXIT-PARA
             UNTIL WS-ANY-MORE-INPUT = 'N'.

           CLOSE FILE3-IN PAYMENT-FILE-OUT.

           DISPLAY 'NUM INPUT RECORDS: ' WS-IN-REC-COUNT.
           DISPLAY 'NUM OUTPUT RECORDS: ' WS-OUT-REC-COUNT.
           DISPLAY 'NUM ERROR RECORDS:' WS-ERR-REC-COUNT.

           STOP RUN.

       100-INITIAL-PARA.
           OPEN INPUT FILE3-IN.
           OPEN OUTPUT PAYMENT-FILE-OUT FILE3-ERR-OUT.
           IF WS-FILE3-STATUS-IN NOT = '00' OR
              WS-PAYMENT-STATUS-OUT NOT = '00' OR
              WS-FILE3-ERR-STATUS-OUT NOT = '00'

             DISPLAY 'ERROR IN 100-INITIAL-PARA'
             DISPLAY 'ERROR OPENING FILE(S)'
             DISPLAY 'FILE3-IN STATUS: ' WS-FILE3-STATUS-IN
             DISPLAY 'PAYMENT-OUT STATUS: ' WS-PAYMENT-STATUS-OUT
             DISPLAY 'FILE3-ERR-OUT STATUS: ' WS-FILE3-ERR-STATUS-OUT
             CALL WS-ABENDPGM
           END-IF.

           INITIALIZE FILE3-LAYOUT-IN PAYMENT-LAYOUT-OUT.

       100-EXIT-PARA. EXIT.

       200-GET-INPUT-PARA.
           READ FILE3-IN
             AT END
               MOVE 'N' TO WS-ANY-MORE-INPUT
               GO TO 200-EXIT-PARA
           END-READ.

           ADD 1 TO WS-IN-REC-COUNT.

       200-EXIT-PARA. EXIT.

       300-PROCESS-PARA.
           PERFORM 310-AUDIT-PARA THRU 310-EXIT-PARA.

           IF WS-HAS-FIELD-ERROR = 'Y'
             PERFORM 340-MOVE-WRITE-ERR-PARA THRU 340-EXIT-PARA
           ELSE
             PERFORM 350-MOVE-WRITE-PARA THRU 350-EXIT-PARA
           END-IF.

           PERFORM 200-GET-INPUT-PARA THRU 200-EXIT-PARA.

       300-EXIT-PARA. EXIT.


       310-AUDIT-PARA.
           MOVE 'Y' TO WS-HAS-FIELD-ERROR.

           IF CARD-NUMBER OF FILE3-LAYOUT-IN = SPACE
             GO TO 310-EXIT-PARA
           END-IF.
           
           IF PAYMENT-AMOUNT OF FILE3-LAYOUT-IN <= 0
             GO TO 310-EXIT-PARA
           END-IF.

           MOVE 'N' TO WS-HAS-FIELD-ERROR.

       310-EXIT-PARA. EXIT.

       340-MOVE-WRITE-ERR-PARA.
           MOVE CARD-NUMBER OF FILE3-LAYOUT-IN
             TO CARD-NUMBER OF FILE3-ERR-LAYOUT-OUT.

           MOVE PAYMENT-AMOUNT OF FILE3-LAYOUT-IN
             TO PAYMENT-AMOUNT OF FILE3-ERR-LAYOUT-OUT.

           WRITE FILE3-ERR-LAYOUT-OUT.
           IF WS-FILE3-ERR-STATUS-OUT NOT = '00'
             DISPLAY 'ERROR IN 340-MOVE-WRITE-ERR-PARA'
             DISPLAY 'WRITING FILE ERROR STATUS: ' 
               WS-FILE3-ERR-STATUS-OUT
             DISPLAY 'CARD-NUMBER: ' CARD-NUMBER OF FILE3-LAYOUT-IN
             CALL WS-ABENDPGM
           END-IF.

           ADD 1 TO WS-ERR-REC-COUNT.

       340-EXIT-PARA. EXIT.

       350-MOVE-WRITE-PARA.
           MOVE CARD-NUMBER OF FILE3-LAYOUT-IN
             TO CARD-NUMBER OF PAYMENT-LAYOUT-OUT.

           MOVE PAYMENT-AMOUNT OF FILE3-LAYOUT-IN
             TO PAYMENT-AMOUNT OF PAYMENT-LAYOUT-OUT.

           WRITE PAYMENT-LAYOUT-OUT.
           IF WS-PAYMENT-STATUS-OUT NOT = '00'
             DISPLAY 'ERROR IN 350-MOVE-WRITE-PARA'
             DISPLAY 'WRITING FILE ERROR STATUS: ' WS-PAYMENT-STATUS-OUT
             DISPLAY 'CARD-NUMBER: ' CARD-NUMBER OF FILE3-LAYOUT-IN
             CALL WS-ABENDPGM
           END-IF.

           ADD 1 TO WS-OUT-REC-COUNT.

       350-EXIT-PARA. EXIT.