       IDENTIFICATION DIVISION.
       PROGRAM-ID. CBLYT09.

       ENVIRONMENT DIVISION.
       INPUT-OUTPUT SECTION.
       FILE-CONTROL.
           SELECT FILE3-OUT ASSIGN TO FILE3
           ORGANIZATION IS SEQUENTIAL
           ACCESS MODE IS SEQUENTIAL
           FILE STATUS IS WS-FILE3-STATUS-OUT.

       DATA DIVISION.
       FILE SECTION.
       FD FILE3-OUT LABEL RECORD IS STANDARD.
       COPY PAYMTCPY REPLACING CC-PAYMENT-LAYOUT BY FILE3-LAYOUT-OUT.

       WORKING-STORAGE SECTION.
       COPY PAYMTCPY REPLACING CC-PAYMENT-LAYOUT BY FILE3-LAYOUT-IN.
       01 WS-FILE3-STATUS-OUT PIC X(02) VALUE SPACE.
       01 WS-IN-REC-COUNT PIC 9(02) VALUE ZERO.
       01 WS-OUT-REC-COUNT PIC 9(02) VALUE ZERO.
       01 WS-ANY-MORE-INPUT PIC X(01) VALUE 'Y'.

       01 WS-ABENDPGM PIC X(08) VALUE 'ABENDPGM'.

       PROCEDURE DIVISION.
       000-MAIN-PARA.
           DISPLAY 'CBLYT09 STARTED'.
           PERFORM 100-INITIAL-PARA THRU 100-EXIT-PARA.
           PERFORM 200-GET-INPUT-PARA THRU 200-EXIT-PARA.
           PERFORM 300-PROCESS-PARA THRU 300-EXIT-PARA
             UNTIL WS-ANY-MORE-INPUT = 'N'.

           CLOSE FILE3-OUT.
           DISPLAY 'NUM INPUT RECORDS: ' WS-IN-REC-COUNT.
           DISPLAY 'NUM OUTPUT RECORDS: ' WS-OUT-REC-COUNT.

           STOP RUN.

       100-INITIAL-PARA.
           OPEN OUTPUT FILE3-OUT.
           IF WS-FILE3-STATUS-OUT NOT = '00'
             DISPLAY 'ERROR IN 100-INITIAL-PARA'
             DISPLAY 'OPENING FILE ERROR STATUS' WS-FILE3-STATUS-OUT
             CALL WS-ABENDPGM
           END-IF.

           INITIALIZE FILE3-LAYOUT-IN FILE3-LAYOUT-OUT.

       100-EXIT-PARA. EXIT.

       200-GET-INPUT-PARA.
           ACCEPT WS-ANY-MORE-INPUT.
           IF WS-ANY-MORE-INPUT NOT = 'Y'
             MOVE 'N' TO WS-ANY-MORE-INPUT
             GO TO 200-EXIT-PARA
           END-IF.

           ACCEPT CARD-NUMBER OF FILE3-LAYOUT-IN.
           ACCEPT PAYMENT-AMOUNT OF FILE3-LAYOUT-IN.

           ADD 1 TO WS-IN-REC-COUNT.

       200-EXIT-PARA. EXIT.

       300-PROCESS-PARA.
           PERFORM 350-MOVE-WRITE-PARA THRU 350-EXIT-PARA.
           PERFORM 200-GET-INPUT-PARA THRU 200-EXIT-PARA.

       300-EXIT-PARA. EXIT.

       350-MOVE-WRITE-PARA.
           MOVE CARD-NUMBER OF FILE3-LAYOUT-IN
             TO CARD-NUMBER OF FILE3-LAYOUT-OUT.

           MOVE PAYMENT-AMOUNT OF FILE3-LAYOUT-IN
             TO PAYMENT-AMOUNT OF FILE3-LAYOUT-OUT.

           WRITE FILE3-LAYOUT-OUT.
           IF WS-FILE3-STATUS-OUT NOT = '00'
             DISPLAY 'ERROR IN 350-MOVE-WRITE-PARA'
             DISPLAY 'WRITING FILE ERROR STATUS: ' WS-FILE3-STATUS-OUT
             DISPLAY 'CARD-NUMBER: ' CARD-NUMBER OF FILE3-LAYOUT-IN
             CALL WS-ABENDPGM
           END-IF.

           ADD 1 TO WS-OUT-REC-COUNT.

       350-EXIT-PARA. EXIT.