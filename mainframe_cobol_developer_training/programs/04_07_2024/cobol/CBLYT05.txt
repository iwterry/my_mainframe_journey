       IDENTIFICATION DIVISION.
       PROGRAM-ID. CBLYT05.

       ENVIRONMENT DIVISION.
       INPUT-OUTPUT SECTION.
       FILE-CONTROL.
           SELECT FILE2-IN ASSIGN TO FILE2
           ORGANIZATION IS SEQUENTIAL
           ACCESS MODE IS SEQUENTIAL
           FILE STATUS IS WS-FILE2-STATUS-IN.

           SELECT TRAN1-FILE-OUT ASSIGN TO TRANSAC1
           ORGANIZATION IS SEQUENTIAL
           ACCESS MODE IS SEQUENTIAL
           FILE STATUS IS WS-TRAN1-STATUS-OUT.

           SELECT FILE2-ERR-OUT ASSIGN TO FILE2ERR
           ORGANIZATION IS SEQUENTIAL
           ACCESS MODE IS SEQUENTIAL
           FILE STATUS IS WS-FILE2-ERR-STATUS-OUT.

       DATA DIVISION.
       FILE SECTION.
       FD FILE2-IN LABEL RECORD IS STANDARD.
       COPY TRANCPY REPLACING CC-TRAN-LAYOUT BY FILE2-LAYOUT-IN.

       FD TRAN1-FILE-OUT LABEL RECORD IS STANDARD.
       COPY TRANCPY REPLACING CC-TRAN-LAYOUT BY TRAN1-LAYOUT-OUT.

       FD FILE2-ERR-OUT LABEL RECORD IS STANDARD.
       COPY TRANCPY REPLACING CC-TRAN-LAYOUT BY FILE2-ERR-LAYOUT-OUT.


       WORKING-STORAGE SECTION.
       01 WS-FILE2-STATUS-IN PIC X(02) VALUE SPACE.
       01 WS-TRAN1-STATUS-OUT PIC X(02) VALUE SPACE.
       01 WS-FILE2-ERR-STATUS-OUT PIC X(02) VALUE SPACE.
       01 WS-ANY-MORE-INPUT PIC X(01) VALUE 'Y'.
       01 WS-HAS-FIELD-ERROR PIC X(01) VALUE 'Y'.
       01 WS-IN-REC-COUNT PIC 9(02) VALUE ZERO.
       01 WS-OUT-REC-COUNT PIC 9(02) VALUE ZERO.
       01 WS-ERR-REC-COUNT PIC 9(02) VALUE ZERO.

       01 WS-ABENDPGM PIC X(08) VALUE 'ABENDPGM'.

       PROCEDURE DIVISION.
       000-MAIN-PARA.
           DISPLAY 'CBLYT05 STARTED'.

           PERFORM 100-INITIAL-PARA THRU 100-EXIT-PARA.
           PERFORM 200-GET-INPUT-PARA THRU 200-EXIT-PARA.
           PERFORM 300-PROCESS-PARA THRU 300-EXIT-PARA
             UNTIL WS-ANY-MORE-INPUT = 'N'.

           CLOSE FILE2-IN
                 TRAN1-FILE-OUT
                 FILE2-ERR-OUT.

           DISPLAY 'TOTAL INPUT RECORD COUNT: ' WS-IN-REC-COUNT.
           DISPLAY 'TOTAL OUTPUT RECORD COUNT: ' WS-OUT-REC-COUNT.
           DISPLAY 'TOTAL ERROR RECORD COUNT: ' WS-ERR-REC-COUNT.

           STOP RUN.

       100-INITIAL-PARA.
           OPEN INPUT FILE2-IN.
           OPEN OUTPUT TRAN1-FILE-OUT FILE2-ERR-OUT.
           IF WS-FILE2-STATUS-IN NOT = '00' OR
              WS-TRAN1-STATUS-OUT NOT = '00' OR
              WS-FILE2-ERR-STATUS-OUT NOT = '00'

             DISPLAY 'ERROR IN 100-INITIAL-PARA'
             DISPLAY 'ERROR OPENING FILE(S)'
             DISPLAY 'FILE2-IN FILE STATUS: ' WS-FILE2-STATUS-IN
             DISPLAY 'TRAN2-OUT FILE STATUS: ' WS-TRAN1-STATUS-OUT
             DISPLAY 'FILE2-ERR FILE STATUS: ' WS-FILE2-ERR-STATUS-OUT
             CALL WS-ABENDPGM
           END-IF.

           INITIALIZE FILE2-LAYOUT-IN
                      TRAN1-LAYOUT-OUT
                      FILE2-ERR-LAYOUT-OUT.

       100-EXIT-PARA. EXIT.

       200-GET-INPUT-PARA.
           READ FILE2-IN
             AT END
               MOVE 'N' TO WS-ANY-MORE-INPUT
               GO TO 200-EXIT-PARA
           END-READ.
           ADD 1 TO WS-IN-REC-COUNT.

       200-EXIT-PARA. EXIT.

       300-PROCESS-PARA.
           PERFORM 310-AUDIT-PARA THRU 310-EXIT-PARA.

           IF WS-HAS-FIELD-ERROR = 'Y'
             PERFORM 340-MOVE-WRITE-ERR-PARA THRU 340-EXIT-PARA
           ELSE
             PERFORM 350-MOVE-WRITE-PARA THRU 350-EXIT-PARA
           END-IF.

           PERFORM 200-GET-INPUT-PARA THRU 200-EXIT-PARA.

       300-EXIT-PARA. EXIT.

       310-AUDIT-PARA.
           MOVE 'Y' TO WS-HAS-FIELD-ERROR.

           IF CARD-NUMBER OF FILE2-LAYOUT-IN = SPACE
             GO TO 310-EXIT-PARA
           END-IF.

           IF TRANS-POS-DETAILS OF FILE2-LAYOUT-IN = SPACE
             GO TO 310-EXIT-PARA
           END-IF.

           IF TRANS-TYPE OF FILE2-LAYOUT-IN = SPACE
             GO TO 310-EXIT-PARA
           END-IF.

           IF TRANS-STATUS OF FILE2-LAYOUT-IN NOT = 'Approved'
             GO TO 310-EXIT-PARA
           END-IF.

           IF TRANS-AMOUNT OF FILE2-LAYOUT-IN <= ZERO
             GO TO 310-EXIT-PARA
           END-IF.

           MOVE 'N' TO WS-HAS-FIELD-ERROR.

       310-EXIT-PARA. EXIT.


       340-MOVE-WRITE-ERR-PARA.
           MOVE CARD-NUMBER OF FILE2-LAYOUT-IN
             TO CARD-NUMBER OF FILE2-ERR-LAYOUT-OUT.
           MOVE TRANS-POS-DETAILS OF FILE2-LAYOUT-IN
             TO TRANS-POS-DETAILS OF FILE2-ERR-LAYOUT-OUT.
           MOVE TRANS-TYPE OF FILE2-LAYOUT-IN
             TO TRANS-TYPE OF FILE2-ERR-LAYOUT-OUT.
           MOVE TRANS-STATUS OF FILE2-LAYOUT-IN
             TO TRANS-STATUS OF FILE2-ERR-LAYOUT-OUT.
           MOVE TRANS-AMOUNT OF FILE2-LAYOUT-IN
             TO TRANS-AMOUNT OF FILE2-ERR-LAYOUT-OUT.
           MOVE TRANS-DATE OF FILE2-LAYOUT-IN
             TO TRANS-DATE OF FILE2-ERR-LAYOUT-OUT.

           WRITE FILE2-ERR-LAYOUT-OUT.
           IF WS-FILE2-ERR-STATUS-OUT NOT = '00'
             DISPLAY 'ERROR IN 340-MOVE-WRITE-ERR-PARA'
             DISPLAY 'WRITING FILE ERROR STATUS: '
               WS-FILE2-ERR-STATUS-OUT
             DISPLAY 'CARD NUMBER: ' CARD-NUMBER OF FILE2-LAYOUT-IN
             CALL WS-ABENDPGM
           END-IF.
           ADD 1 TO WS-ERR-REC-COUNT.

       340-EXIT-PARA. EXIT.


       350-MOVE-WRITE-PARA.
           MOVE CARD-NUMBER OF FILE2-LAYOUT-IN
             TO CARD-NUMBER OF TRAN1-LAYOUT-OUT.
           MOVE TRANS-POS-DETAILS OF FILE2-LAYOUT-IN
             TO TRANS-POS-DETAILS OF TRAN1-LAYOUT-OUT.
           MOVE TRANS-TYPE OF FILE2-LAYOUT-IN
             TO TRANS-TYPE OF TRAN1-LAYOUT-OUT.
           MOVE TRANS-STATUS OF FILE2-LAYOUT-IN
             TO TRANS-STATUS OF TRAN1-LAYOUT-OUT.
           MOVE TRANS-AMOUNT OF FILE2-LAYOUT-IN
             TO TRANS-AMOUNT OF TRAN1-LAYOUT-OUT.
           MOVE TRANS-DATE OF FILE2-LAYOUT-IN
             TO TRANS-DATE OF TRAN1-LAYOUT-OUT.

           WRITE TRAN1-LAYOUT-OUT.
           IF WS-TRAN1-STATUS-OUT NOT = '00'
             DISPLAY 'ERROR IN 350-MOVE-WRITE-PARA'
             DISPLAY 'WRITING FILE ERROR STATUS: ' WS-TRAN1-STATUS-OUT
             DISPLAY 'CARD NUMBER: ' CARD-NUMBER OF FILE2-LAYOUT-IN
             CALL WS-ABENDPGM
           END-IF.
           ADD 1 TO WS-OUT-REC-COUNT.

       350-EXIT-PARA. EXIT.