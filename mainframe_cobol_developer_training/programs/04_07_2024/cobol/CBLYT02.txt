       ID DIVISION.
       PROGRAM-ID. CBLYT02.

       ENVIRONMENT DIVISION.
       INPUT-OUTPUT SECTION.
       FILE-CONTROL.
           SELECT FILE1-IN ASSIGN TO FILE1
           ORGANIZATION IS SEQUENTIAL
           ACCESS MODE IS SEQUENTIAL
           FILE STATUS IS WS-FILE1-STATUS-IN.

           SELECT MASTER1-FILE-OUT ASSIGN TO MASTER1
           ORGANIZATION IS SEQUENTIAL
           ACCESS MODE IS SEQUENTIAL
           FILE STATUS IS WS-MASTER1-STATUS-OUT.

           SELECT FILE1-ERR-OUT ASSIGN TO FILE1ERR
           ORGANIZATION IS SEQUENTIAL
           ACCESS MODE IS SEQUENTIAL
           FILE STATUS IS WS-FILE1-ERR-STATUS-OUT.

       DATA DIVISION.
       FILE SECTION.
       FD FILE1-IN LABEL RECORD IS STANDARD.
       COPY MASTCPY REPLACING CC-LAYOUT BY FILE1-LAYOUT-IN.

       FD MASTER1-FILE-OUT LABEL RECORD IS STANDARD.
       COPY MASTCPY REPLACING CC-LAYOUT BY MASTER1-LAYOUT-OUT.

       FD FILE1-ERR-OUT LABEL RECORD IS STANDARD.
       COPY MASTCPY REPLACING CC-LAYOUT BY FILE1-ERR-LAYOUT-OUT.

       WORKING-STORAGE SECTION.
       01 WS-FILE1-STATUS-IN PIC X(02) VALUE SPACE.
       01 WS-MASTER1-STATUS-OUT PIC X(02) VALUE SPACE.
       01 WS-FILE1-ERR-STATUS-OUT PIC X(02) VALUE SPACE.
       01 WS-IN-REC-COUNT PIC 9(02) VALUE ZERO.
       01 WS-OUT-REC-COUNT PIC 9(02) VALUE ZERO.
       01 WS-ERR-REC-COUNT PIC 9(02) VALUE ZERO.
       01 WS-ANY-MORE-INPUT PIC X(01) VALUE 'Y'.
       01 WS-IS-VALID-INPUT PIC X(01) VALUE 'N'.
       01 WS-ABENDPGM PIC X(08) VALUE 'ABENDPGM'.

       PROCEDURE DIVISION.
       000-MAIN-PARA.
           DISPLAY 'CBLYT01 STARTED'.

           PERFORM 100-INITIAL-PARA THRU 100-EXIT-PARA.
           PERFORM 200-GET-INPUT-PARA THRU 200-EXIT-PARA.
           PERFORM 300-PROCESS-PARA THRU 300-EXIT-PARA
             UNTIL WS-ANY-MORE-INPUT = 'N'.

           CLOSE FILE1-IN MASTER1-FILE-OUT.

           DISPLAY 'TOTAL INPUT RECORDS PROCESSED: ' WS-IN-REC-COUNT.
           DISPLAY 'TOTAL OUTPUT RECORDS PROCESSED: ' WS-OUT-REC-COUNT.
           DISPLAY 'TOTAL ERROR RECORDS PROCESSED: ' WS-ERR-REC-COUNT.

           STOP RUN.

       100-INITIAL-PARA.
           OPEN INPUT FILE1-IN.
           OPEN OUTPUT MASTER1-FILE-OUT FILE1-ERR-OUT.

           IF WS-FILE1-STATUS-IN NOT = '00' OR
              WS-MASTER1-STATUS-OUT NOT = '00' OR
              WS-FILE1-ERR-STATUS-OUT NOT = '00'

             DISPLAY 'ERROR IN INITIAL PARA'
             DISPLAY 'ERROR OPENING FILE(S)'
             DISPLAY 'FILE1 STATUS: ' WS-FILE1-STATUS-IN
             DISPLAY 'MASTER1 STATUS: ' WS-MASTER1-STATUS-OUT
             DISPLAY 'ERROR FILE' WS-FILE1-ERR-STATUS-OUT
             CALL WS-ABENDPGM
           END-IF.

           INITIALIZE FILE1-LAYOUT-IN
                      MASTER1-LAYOUT-OUT
                      FILE1-ERR-LAYOUT-OUT.

       100-EXIT-PARA. EXIT.

       200-GET-INPUT-PARA.
           READ FILE1-IN
              AT END
                MOVE 'N' TO WS-ANY-MORE-INPUT
                GO TO 200-EXIT-PARA
           END-READ.

           ADD 1 TO WS-IN-REC-COUNT.

       200-EXIT-PARA. EXIT.

       300-PROCESS-PARA.
           PERFORM 310-AUDIT-INPUT-PARA THRU 310-EXIT-PARA.

           IF WS-IS-VALID-INPUT = 'Y'
             PERFORM 350-MOVE-WRITE-PARA THRU 350-EXIT-PARA
           ELSE
              PERFORM 360-MOVE-WRITE-ERROR-PARA THRU 360-EXIT-PARA
           END-IF.

           PERFORM 200-GET-INPUT-PARA THRU 200-EXIT-PARA.

       300-EXIT-PARA. EXIT.

       310-AUDIT-INPUT-PARA.
           MOVE 'N' TO WS-IS-VALID-INPUT.

           IF CARD-NUMBER OF FILE1-LAYOUT-IN = SPACE
              GO TO 310-EXIT-PARA
           END-IF.

           IF CUST-NAME OF FILE1-LAYOUT-IN = SPACE
              GO TO 310-EXIT-PARA
           END-IF.

           IF CARD-TYPE IN FILE1-LAYOUT-IN = '1' OR
              CARD-TYPE IN FILE1-LAYOUT-IN = '2' OR
              CARD-TYPE IN FILE1-LAYOUT-IN = '3'
             CONTINUE
           ELSE
             GO TO 310-EXIT-PARA
           END-IF.

           IF REWARD-POINT-CODE OF FILE1-LAYOUT-IN = 'A' OR
              REWARD-POINT-CODE OF FILE1-LAYOUT-IN = 'B' OR
              REWARD-POINT-CODE OF FILE1-LAYOUT-IN = 'C'
             CONTINUE
           ELSE
              GO TO 310-EXIT-PARA
           END-IF.

           IF CUST-ACTIVE-IND OF FILE1-LAYOUT-IN = 'A' OR
              CUST-ACTIVE-IND OF FILE1-LAYOUT-IN = 'I'
             CONTINUE
           ELSE
              GO TO 310-EXIT-PARA
           END-IF.

           MOVE 'Y' TO WS-IS-VALID-INPUT.

       310-EXIT-PARA. EXIT.

       350-MOVE-WRITE-PARA.
           MOVE CARD-NUMBER OF FILE1-LAYOUT-IN
             TO CARD-NUMBER OF MASTER1-LAYOUT-OUT.
           MOVE CUST-NAME OF FILE1-LAYOUT-IN
             TO CUST-NAME OF MASTER1-LAYOUT-OUT.
           MOVE CARD-LIMIT OF FILE1-LAYOUT-IN
             TO CARD-LIMIT OF MASTER1-LAYOUT-OUT.
           MOVE PREVIOUS-DUE OF FILE1-LAYOUT-IN
             TO PREVIOUS-DUE OF MASTER1-LAYOUT-OUT.
           MOVE PREVIOUS-DUE-PAID OF FILE1-LAYOUT-IN
             TO PREVIOUS-DUE-PAID OF MASTER1-LAYOUT-OUT.
           MOVE CARD-TYPE OF FILE1-LAYOUT-IN
             TO CARD-TYPE OF MASTER1-LAYOUT-OUT.
           MOVE REWARD-POINT-CODE OF FILE1-LAYOUT-IN
             TO REWARD-POINT-CODE  OF MASTER1-LAYOUT-OUT.
           MOVE CARD-EXPIRE-DATE OF FILE1-LAYOUT-IN
             TO CARD-EXPIRE-DATE OF MASTER1-LAYOUT-OUT.
           MOVE CUST-ACTIVE-IND OF FILE1-LAYOUT-IN
             TO CUST-ACTIVE-IND OF MASTER1-LAYOUT-OUT.
           MOVE LAST-BILL-DATE OF FILE1-LAYOUT-IN
             TO LAST-BILL-DATE OF MASTER1-LAYOUT-OUT.

           WRITE MASTER1-LAYOUT-OUT.
           IF WS-FILE1-STATUS-IN NOT = '00'
              DISPLAY 'ERROR IN 350-MOVE-WRITE-PARA'
              DISPLAY 'ERROR WRITING TO FILE'
              DISPLAY 'CARD NUMBER: ' CARD-NUMBER OF FILE1-LAYOUT-IN
              DISPLAY 'FILE STATUS: ' WS-MASTER1-STATUS-OUT
              CALL WS-ABENDPGM
           END-IF.

           ADD 1 TO WS-OUT-REC-COUNT.

       350-EXIT-PARA. EXIT.


       360-MOVE-WRITE-ERROR-PARA.
           MOVE CARD-NUMBER OF FILE1-LAYOUT-IN
             TO CARD-NUMBER OF FILE1-ERR-LAYOUT-OUT.
           MOVE CUST-NAME OF FILE1-LAYOUT-IN
             TO CUST-NAME OF FILE1-ERR-LAYOUT-OUT.
           MOVE CARD-LIMIT OF FILE1-LAYOUT-IN
             TO CARD-LIMIT OF FILE1-ERR-LAYOUT-OUT.
           MOVE PREVIOUS-DUE OF FILE1-LAYOUT-IN
             TO PREVIOUS-DUE OF FILE1-ERR-LAYOUT-OUT.
           MOVE PREVIOUS-DUE-PAID OF FILE1-LAYOUT-IN
             TO PREVIOUS-DUE-PAID OF FILE1-ERR-LAYOUT-OUT.
           MOVE CARD-TYPE OF FILE1-LAYOUT-IN
             TO CARD-TYPE OF FILE1-ERR-LAYOUT-OUT.
           MOVE REWARD-POINT-CODE OF FILE1-LAYOUT-IN
             TO REWARD-POINT-CODE  OF FILE1-ERR-LAYOUT-OUT.
           MOVE CARD-EXPIRE-DATE OF FILE1-LAYOUT-IN
             TO CARD-EXPIRE-DATE OF FILE1-ERR-LAYOUT-OUT.
           MOVE CUST-ACTIVE-IND OF FILE1-LAYOUT-IN
             TO CUST-ACTIVE-IND OF FILE1-ERR-LAYOUT-OUT.
           MOVE LAST-BILL-DATE OF FILE1-LAYOUT-IN
             TO LAST-BILL-DATE OF FILE1-ERR-LAYOUT-OUT.

           WRITE FILE1-ERR-LAYOUT-OUT.
           IF WS-FILE1-ERR-STATUS-OUT NOT = '00'
              DISPLAY 'ERROR IN 360-MOVE-WRITE-PARA'
              DISPLAY 'ERROR WRITING TO FILE'
              DISPLAY 'CARD NUMBER: ' CARD-NUMBER OF FILE1-LAYOUT-IN
              DISPLAY 'FILE STATUS: ' WS-MASTER1-STATUS-OUT
              CALL WS-ABENDPGM
           END-IF.

           ADD 1 TO WS-ERR-REC-COUNT.

       360-EXIT-PARA. EXIT.