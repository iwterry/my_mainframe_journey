       IDENTIFICATION DIVISION.
       PROGRAM-ID. CBLYT11.

       ENVIRONMENT DIVISION.
       INPUT-OUTPUT SECTION.
       FILE-CONTROL.
           SELECT MASTER3-FILE-IN ASSIGN TO MASTER3
           ORGANIZATION IS SEQUENTIAL
           ACCESS MODE IS SEQUENTIAL
           FILE STATUS IS WS-MASTER3-STATUS-IN.

           SELECT PAYMENT-FILE-IN ASSIGN TO PAYMENT
           ORGANIZATION IS SEQUENTIAL
           ACCESS MODE IS SEQUENTIAL
           FILE STATUS IS WS-PAYMENT-STATUS-IN.

           SELECT MASTER4-FILE-OUT ASSIGN TO MASTER4
           ORGANIZATION IS SEQUENTIAL
           ACCESS MODE IS SEQUENTIAL
           FILE STATUS IS WS-MASTER4-STATUS-OUT.           

       DATA DIVISION.
       FILE SECTION.
       FD MASTER3-FILE-IN LABEL RECORD IS STANDARD.
       COPY MASTCPY REPLACING CC-LAYOUT BY MASTER3-LAYOUT-IN.

       FD PAYMENT-FILE-IN LABEL RECORD IS STANDARD.
       COPY PAYMTCPY REPLACING CC-PAYMENT-LAYOUT BY PAYMENT-LAYOUT-IN.

       FD MASTER4-FILE-OUT LABEL RECORD IS STANDARD.
       COPY MASTCPY REPLACING CC-LAYOUT BY MASTER4-LAYOUT-OUT.

       WORKING-STORAGE SECTION.
       01 WS-MASTER3-STATUS-IN PIC X(02) VALUE SPACE.
       01 WS-PAYMENT-STATUS-IN PIC X(02) VALUE SPACE.
       01 WS-MASTER4-STATUS-OUT PIC X(02) VALUE SPACE.

       01 WS-IN-REC-COUNT PIC 9(02) VALUE ZERO.
       01 WS-OUT-REC-COUNT PIC 9(02) VALUE ZERO.

       01 WS-ANY-MORE-INPUT-MASTER3 PIC X(01) VALUE 'Y'.
       01 WS-ANY-MORE-INPUT-PAYMENT PIC X(01) VALUE 'Y'.

       01 WS-AMOUNT-PAID PIC S9(05)V9(02) VALUE ZERO.

       01 WS-ABENDPGM PIC X(08) VALUE 'ABENDPGM'.


       PROCEDURE DIVISION.
       000-MAIN-PARA.
           DISPLAY 'CBLYT11 STARTED'.

           PERFORM 100-INITIAL-PARA THRU 100-EXIT-PARA.
           PERFORM 200-GET-INPUT-PARA THRU 200-EXIT-PARA.
           PERFORM 300-PROCESS-PARA THRU 300-EXIT-PARA
             UNTIL WS-ANY-MORE-INPUT-MASTER3 = 'N' AND
                   WS-ANY-MORE-INPUT-PAYMENT = 'N'.

           CLOSE MASTER3-FILE-IN
                 PAYMENT-FILE-IN
                 MASTER4-FILE-OUT.

           DISPLAY 'NUM INPUT RECORDS: ' WS-IN-REC-COUNT.
           DISPLAY 'NUM OUTPUT RECORDS: ' WS-OUT-REC-COUNT.

           STOP RUN.

       100-INITIAL-PARA.
           OPEN INPUT MASTER3-FILE-IN PAYMENT-FILE-IN.
           OPEN OUTPUT MASTER4-FILE-OUT.

           IF WS-MASTER3-STATUS-IN NOT = '00' OR
              WS-PAYMENT-STATUS-IN NOT = '00' OR
              WS-MASTER4-STATUS-OUT NOT = '00'

             DISPLAY 'ERROR IN 100-INITIAL-PARA'
             DISPLAY 'ERROR OPENING FILE(S)'
             DISPLAY 'MASTER3-IN STATUS: ' WS-MASTER3-STATUS-IN
             DISPLAY 'PAYMENT-IN STATUS: ' WS-PAYMENT-STATUS-IN
             DISPLAY 'MASTER4-OUT STATUS: ' WS-MASTER4-STATUS-OUT
             CALL WS-ABENDPGM
           END-IF.

           INITIALIZE MASTER3-LAYOUT-IN
                      PAYMENT-LAYOUT-IN 
                      MASTER4-LAYOUT-OUT.

       100-EXIT-PARA. EXIT.

       200-GET-INPUT-PARA.
           PERFORM 210-GET-INPUT-MASTER3-PARA THRU 210-EXIT-PARA.
           PERFORM 220-GET-INPUT-PAYMENT-PARA THRU 220-EXIT-PARA.

       200-EXIT-PARA. EXIT.

       210-GET-INPUT-MASTER3-PARA.
           READ MASTER3-FILE-IN
             AT END
               MOVE 'N' TO WS-ANY-MORE-INPUT-MASTER3
               GO TO 210-EXIT-PARA
           END-READ.

           ADD 1 TO WS-IN-REC-COUNT.

       210-EXIT-PARA. EXIT.

       220-GET-INPUT-PAYMENT-PARA.
           READ PAYMENT-FILE-IN
             AT END
               MOVE 'N' TO WS-ANY-MORE-INPUT-PAYMENT
           END-READ.

       220-EXIT-PARA. EXIT.

       300-PROCESS-PARA.
           IF CARD-NUMBER OF MASTER3-LAYOUT-IN =
              CARD-NUMBER OF PAYMENT-LAYOUT-IN
              MOVE PAYMENT-AMOUNT TO WS-AMOUNT-PAID
              PERFORM 350-MOVE-WRITE-PARA THRU 350-EXIT-PARA
              PERFORM 200-GET-INPUT-PARA THRU 200-EXIT-PARA
           ELSE
           IF CARD-NUMBER OF MASTER3-LAYOUT-IN <
              CARD-NUMBER OF PAYMENT-LAYOUT-IN
              PERFORM 320-PERFORM-VALID-NOT-EQ-PARA THRU 320-EXIT-PARA
            ELSE
            IF WS-ANY-MORE-INPUT-PAYMENT = 'N'
              PERFORM 320-PERFORM-VALID-NOT-EQ-PARA THRU 320-EXIT-PARA
            ELSE
            IF WS-ANY-MORE-INPUT-MASTER3 = 'N'
              PERFORM 330-HANDLE-ERROR-PARA THRU 330-EXIT-PARA
            ELSE
              PERFORM 330-HANDLE-ERROR-PARA THRU 330-EXIT-PARA
            END-IF
            END-IF
            END-IF
            END-IF.

       300-EXIT-PARA. EXIT.

       320-PERFORM-VALID-NOT-EQ-PARA.
           MOVE PREVIOUS-DUE-PAID OF MASTER3-LAYOUT-IN
             TO WS-AMOUNT-PAID.
           PERFORM 350-MOVE-WRITE-PARA THRU 350-EXIT-PARA.
           PERFORM 210-GET-INPUT-MASTER3-PARA THRU 210-EXIT-PARA.

       320-EXIT-PARA. EXIT.

       330-HANDLE-ERROR-PARA.
           DISPLAY 'ERROR IN 330-HANDLE-ERROR-PARA'.
           DISPLAY 'CARD NUMBER ' CARD-NUMBER OF MASTER3-LAYOUT-IN
                   ' IS NOT PRESENT IS MASTER3-IN'.
           CALL WS-ABENDPGM.

       330-EXIT-PARA. EXIT.

       350-MOVE-WRITE-PARA.
           MOVE CARD-NUMBER OF MASTER3-LAYOUT-IN
             TO CARD-NUMBER OF MASTER4-LAYOUT-OUT.
           MOVE CUST-NAME OF MASTER3-LAYOUT-IN
             TO CUST-NAME OF MASTER4-LAYOUT-OUT.
           MOVE CARD-LIMIT OF MASTER3-LAYOUT-IN
             TO CARD-LIMIT OF MASTER4-LAYOUT-OUT.
           MOVE PREVIOUS-DUE OF MASTER3-LAYOUT-IN
             TO PREVIOUS-DUE OF MASTER4-LAYOUT-OUT.
           MOVE WS-AMOUNT-PAID 
             TO PREVIOUS-DUE-PAID OF MASTER4-LAYOUT-OUT.
           MOVE CARD-TYPE OF MASTER3-LAYOUT-IN
             TO CARD-TYPE OF MASTER4-LAYOUT-OUT.
           MOVE REWARD-POINT-CODE OF MASTER3-LAYOUT-IN
             TO REWARD-POINT-CODE OF MASTER4-LAYOUT-OUT.
           MOVE CARD-EXPIRE-DATE OF MASTER3-LAYOUT-IN
             TO CARD-EXPIRE-DATE OF MASTER4-LAYOUT-OUT.
           MOVE CUST-ACTIVE-IND OF MASTER3-LAYOUT-IN
             TO CUST-ACTIVE-IND OF MASTER4-LAYOUT-OUT.
           MOVE LAST-BILL-DATE OF MASTER3-LAYOUT-IN
             TO LAST-BILL-DATE OF MASTER4-LAYOUT-OUT.

           WRITE MASTER4-LAYOUT-OUT.
           IF WS-MASTER4-STATUS-OUT NOT = '00'
             DISPLAY 'ERROR 350-MOVE-WRITE-PARA'
             DISPLAY 'ERROR WRITING FILE STATUS: ' WS-MASTER4-STATUS-OUT
             DISPLAY 'CARD-NUMBER: ' CARD-NUMBER OF MASTER3-LAYOUT-IN 
             CALL WS-ABENDPGM
           END-IF.

           ADD 1 TO WS-OUT-REC-COUNT.

       350-EXIT-PARA. EXIT.